---
title: PBR 理论基础（一）
date: 2024-05-21 14:20:48
categories: 
  - [图形学]
tags:
  - 图形学
  - 游戏开发
top_img: /images/black.jpg
cover: https://s2.loli.net/2024/05/21/6QnAbYhwJX4Bavl.png
mathjax: true
description: 【长期更新】本笔记的主要内容有 PBR 理论基础（包含光学、物质的光学特性、微平面理论、菲涅尔反射等）、Disney Principled BxDF 相关（包含 BRDF 模型相关公式以及代码，Disney BSDF 的简单介绍）。
---

> 本篇文章主要参考了毛星云的文章：《基于物理的渲染（PBR）白皮书》。本来有了毛星云的文章不打算自己再另写一篇关于 PBR 理论知识的文章了，但是后来还是决定做出一定的更改、补充和简化，方便自己理解。  
> 
> 之前的《Unity Shader入门精要》读书笔记（五）中也有对 PBR 的介绍，建议结合起来看。
> 
> 推荐一个谷歌的 Filament PBR 白皮书：https://google.github.io/filament/Filament.md.html#materialsystem/diffusebrdf 

# PBR 核心理论基础
**基于物理的渲染 Physically Based Rendering，PBR** 是指使用基于物理原理和微平面理论建模的着色/光照模型，以及使用从现实中测量的表面参数来准确表示真实世界材质的渲染理念。可以由三部分组成基于物理的材质 Material，基于物理的光照 Lighting 以及基于物理适配的摄像机 Camera。

PBR 的理论基础主要包括以下内容： 

## 光学相关
### 人眼视觉  
人眼接收到的是物体反射和散射得到的颜色，那些不能被物体吸收 Absorb 的颜色，即被反射或散射到我们人眼中的可见光波长代表的颜色，就是我们能够感知到的物体的颜色。

被反射或散射的光线进入人眼后，首先穿过**角膜 cornea**， 然后进入**瞳孔 pupil**。 随后，光被**晶状体 lens** 折射并撞击**视网膜 retina** 中的两种类型的**感光细胞 photoreceptors**，包括**视锥细胞 cones** 和**视杆细胞 rods**。这些感光细胞从视野范围内吸收光子，然后经一系列特殊复杂的生物化学通路，根据光线中的不同的波长产生不同颜色的视觉信号，波长越高的光偏红，波长越低的光偏蓝。视觉信号通过视神经 optical nerve 传递到视觉皮层 visual cortex，而视觉皮层作为处理视觉信号的大脑区域，用于产生最终的感知图像。上述负责整体人类视觉功能的完整系统被称为**人类视觉系统 Human Visual System，HVS**。不同感光细胞对不同波长的感知敏感度不同。只有可见光才能被人眼感知并处理，而可见光仅覆盖完整电磁波谱在 400 nm 和 700 nm之间非常有限的光谱区间。

### 物理光学 Physical Optics
**波动光学 Wave Optics** 又称**物理光学 Physical Optics**。在波动光学中，光被建模为一种**电磁横波 Transverse Wave**，即电磁波伴随的电场方向、磁场方向和传播方向三者互相垂直。

<div  align="center">  
<img src="https://s2.loli.net/2024/05/21/8hKv6VDsdZjuGyn.png" width = "60%" height = "60%" alt="图1 - 一个简单的光波。它既是单色的（具有单一波长 λ ）又是线性极化的（电场和磁场各自沿单向振荡）"/>
</div>

光波的磁场和电场的振荡相互耦合，且磁场矢量和电场矢量相互垂直，两者长度之比固定，该比率等于**相速度 Phase Velocity**。

而光在传播到两种不同介质交界处时，原始光波和新的光波的相速度 Phase Velocity 的比率定义了介质的光学性质，就是**折射率 Index of Refraction，IOR**，由字母 n 表示。

### 复折射率 complex index of refraction
除了代表光的相速度的实部 $\,n\,$ 之外，还用希腊字母 $\,\kappa\,$（kappa）表示介质将光能转为为其他形式能量的吸收性。$\,n\,$ 和 $\,\kappa\,$ 通常都随波长而变化，两者组合成复数 $\,n + i\kappa\,$，称为**复折射率 complex index of refraction**。

即折射率 IOR 是一个复数，其分为实部和虚部两部分：  
&emsp;&emsp; - 折射率的**实部 real part** 度量了物质如何影响光速，即相对于光在真空中传播速度减慢的度量。  
&emsp;&emsp; - 折射率的**虚部 imaginary part** 确定了光在传播时是否被吸收，转换成其他形式的能量，通常是热能。非吸收性介质虚部为零。

其中，特定材质对光的选择性吸收是因为所选择的光波频率与该材质原子中的电子振动的频率相匹配。由于不同的原子和分子具有不同的固有振动频率，其将选择性地吸收不同频率的可见光。光的吸收对视觉效果有直接影响，因为会降低光的强度，并且如果吸收对某些可见波长具有选择性，也会改变光的颜色。

光在表面的折射条件是在小于单一波长的距离内发生折射率的突然变化。另外，折射率缓慢的逐渐变化不会导致光线的分离，而是导致其传播路径的弯曲。当空气密度因温度而变化时，通常可以看到这种效果，例如海市蜃楼 mirages 和热形变 heat distortion。

### 吸收和散射对外观的影响
光与物质之间的两种相互作用模式为**散射 scattering** 和**吸收 absorption**。  
&emsp;&emsp; - **散射 scattering 决定介质的浑浊程度**。 大多数情况下，固体和液体介质中的颗粒都比光的波长更大，并且倾向于均匀地散射所有可见波长的光。高散射会产生不透明的外观。  
&emsp;&emsp; - **吸收 absorption 决定材质的外观颜色**。几乎任何材质的外观颜色通常都是由其吸收的波长相关性引起的。

而每种介质的外观是散射和吸收两种现象的综合结果，如下图：

<div  align="center">  
<img src="https://s2.loli.net/2024/05/21/qifuDWyvb7mHUrI.png" width = "80%" height = "80%" alt="图2 - 具有不同光的吸收量和散射量的介质。外观取决于散射和吸收两个属性; 例如，白色外观（右下）是高散射和低吸收的结果。"/>
</div>

**材质的最终外观由镜面反射 Specular reflection 以及物质对折射光线的吸收和散射的特性组合综合决定。**

另外，散射和吸收都与观察尺度有关。在小场景中不产生任何明显散射的介质在较大尺度上可能具有非常明显的散射。例如，当观察房间中的一杯水时，在空气中的光的散射和在光在水中的吸收都是不可见的。但是，水在数米的距离内对光线进行吸收，特别是对于红色波长对应的光的吸收非常强烈。又例如，在多英里的空气中会有明显的光的散射。

### 几何光学 Geometrical Optics
<div  align="center">  
<img src="https://s2.loli.net/2024/05/21/ru9LeBpv2w6tg4U.webp" width = "80%" height = "80%" alt="图3 - 光与物体表面的交互图示。（来自 GDC 2016，An End-to-End Approach to Physically Based Rendering）"/>
</div>

当一束光线入射到物体表面时，由于物体表面与空气两种介质之间折射率的快速变化，光线会发生反射和折射：  
①**镜面反射 Specular Reflection**：即光线在两种介质交界处的直接反射，金属的镜面反射颜色为三通道的彩色，而非金属的镜面反射颜色为单通道的单色。镜面反射占入射光线的比率由菲涅耳方程计算而得，镜面反射的集中度由微平面的表面粗糙度决定，微平面的表面粗糙度越大（越不规则），反射光线的分散程度越强，反射越模糊；  
②**折射 Refraction**：从表面折射入介质的光，会发生吸收 absorption 和散射 scattering，而介质的整体外观由其散射和吸收特性的组合决定，其中：  
&emsp;&emsp; - **散射 Scattering**：折射率的快速变化引起散射，光的方向会改变（分裂成多个方向），但是光的总量或光谱分布不会改变。散射最终被视作的类型与观察尺度有关，包括：  
&emsp;&emsp;&emsp;&emsp; a. **次表面散射 Subsurface Scattering**：观察像素小于散射距离，散射被视作次表面散射。  
&emsp;&emsp;&emsp;&emsp; b. **漫反射 Diffuse**：观察像素大于散射距离，散射被视作漫反射。  
&emsp;&emsp;&emsp;&emsp; c. **透射 Transmission**：入射光经过折射穿过物体后的出射现象。透射为次表面散射的特例。  
&emsp;&emsp; - **吸收 Absorption**：具有复折射率的物质区域会引起吸收，具体原理是光波频率与该材质原子中的电子振动的频率相匹配。复折射率 complex number 的虚部 imaginary part 确定了光在传播时是否被吸收（转换成其他形式的能量）。发生吸收的介质的光量会随传播的距离而减小（如果吸收优先发生于某些波长，则可能也会改变光的颜色），而光的方向不会因为吸收而改变。任何颜色色调通常都是由吸收的波长相关性引起的。

### 物质的光学特性 Substance Optical Properties
现实世界中有不同类型的物质可分为三大类：**绝缘体 Insulators**，**半导体 semi-conductors** 和**导体 conductors**。在渲染和游戏领域，我们一般只对其中的两个感兴趣：导体（金属）和绝缘体（电介质，非金属）：  
①**金属 Metal**：金属的外观主要取决于光线在两种介质的交界面上的直接反射（即镜面反射）。**金属的镜面反射颜色为三通道的彩色，R、G、B 各不相同。**而折射入金属内部的光线几乎立即全部被自由电子吸收，且折射入金属的光不存在散射。  
②**非金属 No-Metal**：即**绝缘体 Insulators** 或**电介质 Dielectric**，其的整体外观主要由其吸收和散射的特性组合决定。**非金属的镜面反射颜色为单通道单色，即 R = G = B。**光从表面折射入非金属介质，则介质的整体外观由其散射和吸收的特性组合决定。不同的介质类型的散射和吸收特性不一，按介质类型的散射和吸收特性，可以分为多类：  
&emsp;&emsp; - **均匀介质 Homogeneous Media**：主要为透明介质，无折射率变化。不存在散射，光总以直线传播并且不会改变方向。存在吸收，光的强度会通过吸收减少，传播距离越远，吸收量越高。  
&emsp;&emsp; - **非均匀介质 Nonhomogeneous Media**：通常可以建模为具有嵌入散射粒子的均匀介质。具有折射率变化，又分为以下几类：  
&emsp;&emsp;&emsp;&emsp; a. **混浊介质 Cloudy Media**：混浊介质具有弱散射，散射方向略微随机化。根据组成的不同，具有复数折射率的物质区域引起吸收。   
&emsp;&emsp;&emsp;&emsp; b. **半透明介质 Translucent Media**：半透明介质具有强散射，散射方向完全随机化。根据组成的不同，具有复数折射率的物质区域引起吸收。  
&emsp;&emsp;&emsp;&emsp; c. **不透明介质 Opaque Media**：不透明介质和半透明介质一致。具有强散射，散射方向完全随机化。根据组成的不同，具有复数折射率的物质区域引起吸收。 


## 微平面理论 Microfacet Theory  
该理论认为物体表面做无数微观尺度上有随机朝向的理想镜面反射的小平面 microfacet 的理论。在实际的 PBR 工作流中，这种物体表面的不规则性用粗糙度贴图或者高光度贴图来表示。  

光在与**非光学平坦表面 Non-Optically-Flat Surfaces** 的交互时，非光学平坦表面表现得像一个微小的光学平面表面的大集合。表面上的每个点都会以略微不同的方向对入射光反射，而最终的表面外观是许多具有不同表面取向的点的聚合结果。出于着色的目的，通常会用统计方法处理这种微观几何现象，将表面视为具有微观结构法线的随机分布，并将宏观表面视为在每个点处多个方向上反射（和折射）光的集合。在微观尺度上，表面越粗糙，反射越模糊，表面越光滑，反射越集中。

上面说的是微表面的反射，下面讨论被微表面折射的光。从表面折射的光取决于对象本身的特性：  
&emsp;&emsp; - 对于**金属**，折射光会立刻被吸收，能量被自由电子立即吸收。  
&emsp;&emsp; - 对于**非金属**（电介质或绝缘体），一旦光在其内部折射，就表现为常规的参与介质，表现出吸收和散射两种行为。

另外，漫反射和**次表面散射 Subsurface Scattering，SSS** 其实是相同物理现象，本质都是折射光的次表面散射的结果。唯一的区别是相对于观察尺度的散射距离。散射距离相较于像素来说微不足道，次表面散射便可以近似为漫反射。也就是说，光的折射现象，建模为漫反射还是次表面散射，取决于观察的尺度。

## 菲涅尔反射 Fresnel Reflectance
光线以不同角度入射会有不同的反射率。相同的入射角度，不同的物质也会有不同的反射率。万物皆有菲涅尔反射。F0是即 0 度角入射的菲涅尔反射值。大多数非金属的 F0 范围是 0.02 ~ 0.04，大多数金属的 F0 范围是 0.7 ~ 1.0。 

### 菲涅尔效应 Fresnel Effect
菲涅尔效应 Fresnel effect 作为基于物理的渲染理念中的核心理念之一，表示的是看到的光线的反射率与视角相关的现象，其具体表现是在掠射角（与法线呈接近 90 度）下光的反射率会增加。

<div  align="center">  
<img src="https://s2.loli.net/2024/05/22/UOaeGA9cd7ZwhEL.webp" width = "70%" height = "70%" alt="图4 - 菲涅尔效应"/>
</div>

菲涅尔效应即描述视线垂直于表面时反射较弱，而当视线非垂直表面时，夹角越小，反射越明显的一种现象。比如说，当我们站在湖边低头看脚下的湖水，会发现水是透明的，反射不会特别强烈；而如果我们看远处的湖面时，会发现水并不透明，而且反射非常强烈。

需要注意的是，我们在宏观层面看到的菲涅尔效应实际上是微观层面微平面菲涅尔效应的平均值。也就是说，影响菲涅尔效应的关键参数在于每个微平面的法向量和入射光线的角度，而不是宏观平面的法向量和入射光线的角度。当从接近平行于表面的视线方向进行观察，所有光滑表面都会变得 100% 的反射性。对于粗糙表面来说，在接近平行方向的高光反射也会增强，但不够达到 100% 的强度。

不同材质的菲涅尔效应的强弱是不同的，导体（如金属）的菲涅尔效应一般很弱，主要是因为导体本身的反射率就已经很强。就拿铝来说，其反射率在所有角度下几乎都保持 86% 以上，随角度变化很小，而绝缘体材质的菲涅尔效应就很明显，比如折射率为 1.5 的玻璃，在表面法向量方向的反射率仅为 4%，但当视线与表面法向量夹角很大的时候，反射率可以接近 100%，这一现象也导致了金属与非金属外观上的不同。

### 菲涅尔方程 Fresnel Equations
菲涅尔方程是描述光在不同折射率的介质之间的行为的方程。菲涅尔方程描述的光的反射现象便被称之为菲涅尔反射。菲涅尔方程能解释反射光的强度、折射光的强度、相位与入射光的强度的关系。

菲涅尔方程和**麦克斯韦方程组 Maxwell’s equations** 有很深的渊源。根据物理学，麦克斯韦方程组可以在折射率变化时计算光的行为。对于空气中通常的物体表面而言，物体的表面便是空气折射率和物体折射率的交界面，对此特殊的折射率交界面而言，麦克斯韦方程组的解被我们称为菲涅尔方程。即菲涅尔反射的方程可以通过麦克斯韦电磁学方程组推导出来，因为本质上讲菲涅尔反射其实是使用的波动理论来解释光的反射现象。

完整的菲涅尔方程有点复杂，至少可以说艺术家所需的材质参数（在可见光谱上密集采样的复折射率）并不方便。而通过完整的菲涅尔方程，我们可以拟合出更简化的近似表达式（如 Schlick\[1994\] 的 Fresnel 近似），以及抽象出描述物体表面属性的菲涅尔反射率 F0。

<div  align="center">  
<img src="https://s2.loli.net/2024/05/22/RXKuOFW1h5dLg6H.webp" width = "60%" height = "60%" alt="图5 - 各种物质的菲涅尔反射率（y 轴）作为入射角（x 轴）的函数。由于铜和铝在可见光谱上的反射率有明显变化，因此它们的反射率显示为 R，G 和 B 的三条独立曲线。铜的 R 曲线最高，其次是 G，最后是 B（因此它的红色）。铝的 B 曲线最高，其次是 G，最后是 R。上图选择的材质代表了各种各样的材质。尽管如此，可以看到一些共同的地方。对于 0° 和 45° 之间的入射角，反射率几乎是恒定的。在 45° 和 75° 之间，反射率变化更为显着（通常但不总是有所增加）。最后，在 75° 和 90° 之间，反射率总是迅速变为 1。（图片来自 Hoffman N. Background: Physics and Math of Shading）"/>
</div>

当光线垂直（以 0 度角）撞击表面时，该光线被反射 Reflected 为镜面反射光的比率被称为 **F0**。即 F0 为 0 度角入射时的菲涅尔反射率。而折射 refracted 到表面中的光量则为 1 - F0。大多数常见电介质的 F0 范围为 0.02 - 0.05（线性值）。 对于导体，F0 范围为 0.5 - 1.0。

F0 是一个反射系数，可以通过折射率 n 来计算（Schlick’s approximation 中的计算，只适用于对电介质的 F0 的估计）：  

$$ F_0 = (\frac {n_1 - n_2} {n_1 + n_2})^2 $$

其中，n1 和 n2 分别为两种介质的折射率。通常假设 n1 = 1 近似于空气的折射率，并用 n 替换 n2 ，于是，上式可以简化为：  

$$ F_0 = (\frac {n - 1} {n + 1})^2 $$

一个查折射率 IOR 的网站：https://pixelandpoly.com/ior.html 。对于一般仅有实数折射率的非金属而言，可以通过查询到的物质的折射率和上面的公式，计算出 F0。

### 常见材质 F0 特性总结

<div  align="center">  
<img src="https://s2.loli.net/2024/05/22/71tFO6mnv2iz9fW.png" width = "80%" height = "80%" alt="图6 - 常见材质 F0 参考速查图表"/>
</div>

需要注意，我们一般使用的 F0，都是空气对于材质的F0。假如材质位于水中或其他折射率不等于 1 的介质中，F0 会发生变化。

**①金属 Metal**  
&emsp;&emsp; - 金属具有高的 F0 值 - 几乎总是 0.5 或更高。一些金属具有在可见光谱范围内变化的光学性质，导致了这些金属有色的菲涅尔反射率。  
&emsp;&emsp; - 黄金有一个不寻常的 F0 值，是最亮的金属之一。其红色通道值略高于 1，而且具有特别低的蓝色通道值（低于 0.5）。  
&emsp;&emsp; - 金属会立即吸收任何透射光，因此它们不会出现任何次表面散射或透明感。  
&emsp;&emsp; - 金属的所有可见颜色都来自 F0。

**②电介质 Dielectrics**  
&emsp;&emsp; - 日常生活中遇到的大多数材质都是电介质，比如玻璃，皮肤，木头，头发，皮革，塑料，石头和混凝土等等。水其实也是电介质（绝缘体），水的导电性是由于水中的各种杂质造成的。  
&emsp;&emsp; - 电介质具有相当低的 F0 值 - 通常为 0.06 或更低。在垂直入射时的这种低反射率使得菲涅尔效应对于电介质尤其明显。电介质的光学性质在可见光谱上很少变化很大，导致无色反射率值。  
&emsp;&emsp; - 水晶和宝石，其实大多数情况下组成成分是石英 Quartz，而石英的主要化学成分为 SiO2，也是电介质（绝缘体）。石英根据颜色，可以分为：紫水晶、黄水晶、粉晶、烟水晶、乳石英、白水晶等。  

**③半导体 Semiconductor**  
&emsp;&emsp; - 常见的半导体材质有硅、锗、砷化镓等。  
&emsp;&emsp; - 正如半导体电导率位于绝缘体至导体之间一样，其 F0 值也位于最亮的电介质和最暗的金属之间。  


## 其他相关理论
①**能量守恒 Energy Conservation**
出射光线的能量永远不能超过入射光线的能量。随着粗糙度的上升镜面反射区域的面积会增加，作为平衡，镜面反射区域的平均亮度则会下降。  

②**线性空间 Linear Space**
光照计算必须在线性空间完成，shader 中输入的 gamma 空间的贴图比如漫反射贴图需要被转成线性空间，在具体操作时需要根据不同引擎和渲染器的不同做不同的操作。而描述物体表面属性的贴图如粗糙度，高光贴图，金属贴图等必须保证是线性空间。 

③**色调映射 Tone Mapping**  
也称色调复制 tone reproduction，是将宽范围的照明级别拟合到屏幕有限色域内的过程。因为基于 HDR 渲染出来的亮度值会超过显示器能够显示最大亮度，所以需要使用色调映射，将光照结果从 HDR 转换为显示器能够正常显示的 LDR。  

# 渲染方程与 BxDF 简介
## 渲染方程和反射方程
[GAMES101-图形学入门公开课笔记（二）](https://ybniaobu.github.io/2024/04/23/2024-04-23-GAMES_101_2/) 的第十五课 Ray Tracing III 里都有讲过。

某一点 p 的**渲染方程**，可以表示为：

$$ L_o = L_e + \int_{\Omega} f_r \cdot L_i \cdot (w_i \cdot n) \cdot dw_i $$

其中 $\,L_o\,$ 是 p 点的出射光亮度；$\,L_e\,$ 是 p 点的自发光亮度；$\,f_r\,$ 是 p 点入射方向到出射方向光的反射比例，即 BxDF，一般为 BRDF；$\,L_i\,$ 是 p 点的入射光亮度；$\,(w_i \cdot n)\,$ 是入射角带来的入射光衰减。

**反射方程** 就是上面的方程的后半段，即不包括 Emission term 的渲染方程。

## BxDF
BxDF 一般而言是对 BRDF、BTDF、BSDF、BSSRDF 等几种双向分布函数的一个统一的表示。其中，BSDF 可以看做 BRDF 和 BTDF 更一般的形式，而且 BSDF = BRDF + BTDF。

①**BRDF**：双向反射分布函数 Bidirectional Reflectance Distribution Function；  
②**BSDF**：双向散射分布函数 Bidirectional Scattering Distribution Function；  
③**BTDF**：双向透射分布函数 Bidirectional Transmittance Distribution Function；  
④**BSSRDF**：双向散射表面反射分布函数 Bidirectional Scattering-Surface Reflectance Distribution Function。  

BSSRDF 和 BRDF 的不同之处在于，BSSRDF 可以指定不同的光线入射位置和出射位置。BRDF 基本足够用了，BSDF、BTDF、BSSRDF 往往更多用于半透明材质和次表面散射材质等。我们时常讨论的PBR 中的 BxDF，一般都为 BRDF，对于进阶的一些材质的渲染，才会讨论 BSDF 等其他三种 BxDF。

<div  align="center">  
<img src="https://s2.loli.net/2024/05/24/ZsmBauNTKxSoYHL.webp" width = "60%" height = "60%" alt="图7 - BSDF：BRDF + BTDF"/>
</div>

而 BRDF 的分类如下图：

<div  align="center">  
<img src="https://s2.loli.net/2024/05/24/2CqQHcdnfiwkKI8.webp" width = "90%" height = "90%" alt="图8 - BRDF 的分类，来自[Montes R, Ureña C. An overview of BRDF models[J]. 2012]"/>
</div>

# Disney Principled BxDF
迪士尼动画工作室在 SIGGRAPH 2012 上著名的演讲《Physically-based shading at Disney》中提出了迪士尼原理化 BRDF（Disney Principled BRDF），奠定了后续游戏行业和电影行业 PBR 的方向和标准。了解 Disney Principled BxDF，是深入理解 PBR 的重要一环。

随后的 2015 年，迪士尼动画工作室在 Disney Principled BRDF 的基础上进行了修订，提出了Disney Principled BSDF [Extending the Disney BRDF to a BSDF with Integrated Subsurface Scattering, 2015]。

在迪士尼原理化 BRDF 之前，基于物理的渲染都需要大量复杂而不直观的参数，而迪士尼的模型是**艺术导向 Art Directable** 的，而不是完全物理正确的 physically correct。正因为这种艺术导向的易用性，能让艺术家用非常直观的少量参数，以及非常标准化的工作流，就能快速实现涉及大量不同材质的真实感的渲染工作。从本质上而言，Disney Principled BRDF 模型是金属和非金属的混合型模型，最终结果是基于金属度 metallice 在金属 BRDF 和非金属 BRDF 之间进行线性插值。也正因如此，在 PBR 的金属/粗糙度工作流中，固有色 baseColor 贴图才会同时包含金属和非金属的材质数据：金属的反射率值、非金属的漫反射颜色。

在 2012 年受到 Disney 的启发后，以下是主流游戏引擎从传统渲染转移到基于物理的渲染时间节点：  
[SIGGRAPH 2013] UE4 ：[Real shading in unreal engine 4]；  
[SIGGRAPH 2014] Frostbite（寒霜）：[Moving Frostbite to PBR]；  
[GDC 2014] Unity ：[Physically Based Shading in Unity]。

## Disney Principled BRDF 参数
Disney Principled BRDF 包含十一个参数：  
- **baseColor 基础色**：表面颜色，保存了金属的反射率和非金属的反射颜色，通常由纹理贴图提供。  
- **subsurface 次表面**：使用次表面近似控制漫反射形状。  
- **metallic 金属度**：金属（0 = 电介质，1 = 金属）。这是两种不同模型之间的线性混合。金属模型没有漫反射成分，并且还具有等于基础色 baseColor 的着色入射镜面反射。  
- **specular 镜面反射强度**：入射镜面反射量。用于取代折射率。  
- **specularTint 镜面反射颜色**：对美术控制的让步，用于对基础色 baseColor 的入射镜面反射进行颜色控制。掠射镜面反射仍然是非彩色的。  
- **roughness 粗糙度**：表面粗糙度，控制漫反射和镜面反射。  
- **anisotropic 各向异性强度**：用于控制镜面反射高光的纵横比。（0 = 各向同性，1 = 最大各向异性）
- **sheen 光泽度**：一种额外的掠射分量 grazing component，主要用于布料。  
- **sheenTint 光泽颜色**：对 sheen 光泽度的颜色控制。  
- **clearcoat 清漆强度**：有特殊用途的第二个镜面波瓣 specular lobe。
- **clearcoatGloss 清漆光泽度**：控制透明涂层光泽度，0 = 缎面 satin 外观，1 = 光泽 gloss 外观。

<div  align="center">  
<img src="https://s2.loli.net/2024/05/24/LjbSYqKUo1khgOI.png" width = "90%" height = "90%" alt="图9 - Disney Principled BRDF 的参数效果"/>
</div>

## 迪士尼对 MERL 材质数据库的观察结论
在 BRDF 可视化方面，迪士尼提供了一些工具和资源：  
①MERL 100 BRDF 材质库。Matusik等人[Matusik et al.2003]捕获的一组100个各向同性BRDF材质样本库。涵盖了各种材质，包括油漆，木材，金属，织物，石材，橡胶，塑料和其他合成材质。对学术与研究免费授权。  
&emsp;&emsp; - MERL BRDF主站：http://merl.com/brdf  
&emsp;&emsp; - Database地址：https://people.csail.mit.edu/wojciech/BRDFDatabase/  
②BRDF Explorer。迪士尼为分析、比较和新开发 BRDF 模型而开发的可视化工具。该工具在分析测量材质，比较现有模型，以及开发新模型方面具有无可估量的价值。  
&emsp;&emsp; - 官方主页：https://www.disneyanimation.com/technology/brdf.html  
&emsp;&emsp; - GitHub地址：https://github.com/wdas/brdf  
③BRDF Image Slice 切片。将 θh 与 θd 作为横轴和纵轴，对观察到的材质的 BRDF 进行建模的 2D 图像切片。如下图：

<div  align="center">  
<img src="https://s2.loli.net/2024/06/02/kj9nKNOrb74VGCX.webp" width = "90%" height = "90%" alt="图10 - BRDF 图像切片"/>
</div>

### Diffuse 项的观察结论
①漫反射 Diffuse 表示折射 refracted 到表面，经过散射 scattered 和部分吸收 partially absorbed，最终重新出表面出射的光。  
②被着色的非金属材质的任意出射部分都可以视为漫反射。  
③通过观察得出**掠射逆反射 grazing retroreflection** 有明显的着色现象，即可以将掠射逆反射 grazing retroreflection 也看做一种漫反射现象。（注：掠射逆反射是一种特殊的反射现象，当光线以接近掠射角（接近90度）的角度入射到表面时，反射光线会沿着与入射光线几乎相反的方向返回）  
④粗糙度会对菲涅尔折射造成影响，而一般的漫反射模型如 Lambert 忽略了这种影响。  
⑤Oren-Nayar 模型（1995）预测粗糙漫反射表面逆向反射的增加会使漫反射形状变平。然而，其逆向反射波峰不像测量数据那样强，并且粗糙测量的材质通常不显示漫反射的平坦化。  
⑥Hanrahan-Krueger 模型（1993），源自次表面散射理论，也预测了漫反射形状的平坦化，但在边缘处没有足够强的峰值。与 Oren-Nayar 相比，该模型呈现出完美光滑的表面。

下图中比较了 Oren-Nayar、Hanrahan-Krueger 和 Lambert 模型：  

<div  align="center">  
<img src="https://s2.loli.net/2024/06/02/3f5lR7vZYKSNpXz.webp" width = "60%" height = "60%" alt="图11 - Lambert，Oren-Nayar 和 Hanrahan-Krueger 漫反射模型的 BRDF 切片和点光源响应"/>
</div>

### Specular D 项的观察结论
①微观/法线分布函数 $\,D(θ_h)\,$ 可以从测量材质的逆反射 retroreflective 响应观察得到。  
②绝大多数 MERL 材质都有**镜面波瓣 specular lobes**，且尾部比传统的镜面模型长得多。 即反射分布项需要更宽的尾部。  
③GGX 比其他分布具有更长的尾部，但仍然无法捕捉到铬金属 chrome 样本的闪亮亮点。

<div  align="center">  
<img src="https://s2.loli.net/2024/06/02/XyYI4iuSFZTm5rd.webp" width = "80%" height = "80%" alt="图12 - MERL 铬金属（chrome）与几个镜面分布的比较。 左：镜面波峰的对数比例图）; 黑色曲线表示 MERL 铬金属（chrome），红色曲线表示 GGX 分布（α = 0.006），绿色曲线表示 Beckmann 分布（m = 0.013），蓝色曲线表示 Blinn Phong（n = 12000），其中，绿色曲线和蓝色曲线基本重合。右：chrome、GGX 和 Beckmann 分布的点光源响应。"/>
</div>

### Specular F 项的观察结论
①菲涅尔反射系数 $\,F(θ_d)\,$ 表示了光照方向和观察方向夹角逐渐增大时高光/镜面反射强度的增加。  
②光滑表面在切线入射时有将接近 100％ 的镜面反射。  
③对于粗糙表面，无法实现100％的镜面反射，但反射率仍会将变得越来越高。  
④每种材质在掠射角附近都显示出一些反射率的增加。  
⑤掠射角入射附近的许多曲线的陡度已经大于菲涅尔效应的预测值。

### Specular G 项的观察结论
①几何项的影响可以间接地看作其对方向反射率 directional albedo 的影响；  
②大多数材质的方向反射率 directional albedo 对于前 70 度是相对平坦的，并且切线入射处的反射率与表面粗糙度密切相关。  
③几何项的选择会对反射率产生影响，反过来又会对表面外观产生影响。  
④完全省略 G 项和 $\,1/(cosθ_l \cdot cosθ_v)\,$ 项的模型，被称为 “No G” 模型，会导致在掠射角处过暗的响应。

<div  align="center">  
<img src="https://s2.loli.net/2024/06/02/AhyXY6ZvbOl3sPN.webp" width = "80%" height = "80%" alt="图13 - 几种镜面反射几何模型的反射率图示。所有图中都使用相同的 D（GGX/TR）项和 F 项。左图：光滑表面（α = 0.02）; 右图：粗糙表面（α = 0.5）"/>
</div>

### 布料 Fabric 材质的观察结论
①许多布料样本在掠射角处呈现出镜面反射的色调，并且还具有比具有十分粗糙的材质更强的菲涅尔波峰。  
②布料具有有色的掠射反射，可以理解为是其轮廓附近获取到材质颜色的透射纤维 transmissive fibers 造成的。  
③布料在掠射角处的额外光泽增加，超出了普通微平面模型的预测范围。

### 彩虹色 Iridescence 的观察结论
光盘、镭射、昆虫甲壳、油膜、肥皂泡的五彩斑斓的效果都基于共同的物理原理，从视觉的角度我们称之为**彩虹色 iridescence**。  
①变色涂料 color-changing-paint 在 (θh,θd) 空间上显示出连续的色块，且对 φd 的依赖性最小。  
②彩虹色远离镜面峰值的反射率非常小，所以可以将彩虹色理解为一种镜面反射现象。  
③可以将镜面色调调制为 θh 和 θd 的函数，配合小尺寸纹理贴图对彩虹色进行建模。  

## Disney Principled BRDF 的着色模型
### 核心 BRDF 模型
Disney 采用了通用的 microfacet Cook-Torrance BRDF 着色模型：

> Cook-Torrance BRDF 模型的镜面反射项是根据 Torrance-Sparrow BRDF 描述的完整各向同性材质反射模型，再将他应用于图形学得到的。

$$ f(l,v) = diffuse + \frac {D(\theta_h)F(\theta_d)G(\theta_l, \theta_v)} {4 \, cos \theta_l \, cos \theta_v} $$

后面部分即镜面反射项，D 为法线分布函数，主要负责镜面反射波峰 specular peak 的形状；F 为菲涅尔反射系数；G 为几何衰减 geometric attenuation /阴影项 shadowing factor。

### Disney Diffuse
根据对 Merl 100 材质库的观察，发现粗糙表面在掠射角容易形成一条亮边，而相反地光滑表面则容易在掠射角形成一条阴影边。于是 Disney 开发了一种用于漫反射的新的经验模型，以在光滑表面的漫反射菲涅尔阴影和粗糙表面之间进行平滑过渡。

Disney 使用了 Schlick Fresnel 近似，并修改掠射逆反射 grazing retroreflection response 以达到其特定值由粗糙度值确定，而不是简单为 0。公式如下：  

$$ f_d = \frac {baseColor} {\pi} (1 + (F_{D90} - 1)(1 - cos \theta_l)^5) (1 + (F_{D90} - 1)(1 - cos \theta_v)^5) $$

$$ 其中，F_{D90} = 0.5 + 2 \cdot roughness \cdot cos^2 \theta_d $$

$\,\theta_d\,$ 是入射方向 l 和半角向量 h 的夹角，以下为上述 Disney Diffuse 的 Shader 实现代码：  

    // [Burley 2012, "Physically-Based Shading at Disney"]
    float3 Diffuse_Burley_Disney( float3 DiffuseColor, float Roughness, float NoV, float NoL, float LoH )
    {
      float FD90 = 0.5 + 2 * LoH * LoH * Roughness;
      float FdV = 1 + (FD90 - 1) * Pow5( 1 - NoV );
      float FdL = 1 + (FD90 - 1) * Pow5( 1 - NoL );
      return DiffuseColor * ( (1 / PI) * FdV * FdL );
    }

> Schlick Fresnel 近似式原本是 $\,v \cdot n\,$，但根据微面元理论，微面元的法线 m 并不等于 n，m 是 l 和 v 的一半，即半角向量 h。故 $\,v \cdot n\,$ 变为了 $\,v \cdot h\,$，$\,v \cdot h\,$ 和 $\,l \cdot h\,$ 是一样的。

### 法线分布项 Specular D：GTR
在流行的模型中，GGX 拥有最长的尾部。而 GGX 其实与 Blinn (1977) 推崇的 Trowbridge-Reitz（TR）(1975) 分布等同。然而，对于许多材质而言，即便是 GGX 分布，仍然没有足够长的尾部。

**GGX** 或 **Trowbridge-Reitz（TR）**的公式为：

$$ D_{GGX/TR} = c / (\alpha^2 cos^2 \theta_h + sin^2 \theta_h)^2 $$

c 为缩放常数 scaling constant；α 为粗糙度参数 roughness parameter ，其值在 0 和 1 之间; α = 0 产生完全平滑的分布。

来自 Berry(1923) 的分布函数和 Trowbridge-Reitz 分布具有非常相似的形式，但指数为 1 而不是 2，从而导致了更长的尾部：

$$ D_{Berry} = c / (\alpha^2 cos^2 \theta_h + sin^2 \theta_h) $$

通过，Trowbridge-Reitz 和 Berry 的形式的对比，Disney 发现其具有相似的形式，只是幂次不同，于是，Disney 将 Trowbridge-Reitz 进行了 N 次幂的推广，并将其取名为 **Generalized-Trowbridge-Reitz，GTR**：

$$ D_{GTR} = c / (\alpha^2 cos^2 \theta_h + sin^2 \theta_h)^{\gamma} $$

可以发现，上式中：$\,\gamma = 1\,$ 时，GTR 即 Berry 分布；$\,\gamma = 2\,$ 时，GTR 即 Trowbridge-Reitz 分布。

<div  align="center">  
<img src="https://s2.loli.net/2024/06/04/uSH1YEh3cyek5zs.webp" width = "70%" height = "70%" alt="图14 - 各种 γ 值的 GTR 分布曲线与 θh 的关系"/>
</div>

另外，Disney Principled BRDF 中使用了两个固定的**镜面反射波瓣 specular lobe**，且都使用 GTR 模型，如下：  
①**主波瓣 primary lobe** 使用 γ = 2 的 GTR（即 GGX 分布），代表基础底层材质 Base Material 的反射，可为各项异性 anisotropic 或各项同性 isotropic 的金属或非金属；  
②**次级波瓣 secondary lobe** 使用 γ = 1 的 GTR（即 Berry 分布），代表基础材质上的清漆层 ClearCoat Layer 的反射，一般为各项同性 isotropic 的非金属材质，即清漆层。

以下是 γ = 1 和 γ = 2 时 GTR 分布的 Shader 实现代码：  

    // Generalized-Trowbridge-Reitz distribution
    float D_GTR1(float alpha, float dotNH)
    {
        float a2 = alpha * alpha;
        float cos2th = dotNH * dotNH;
        float den = (1.0 + (a2 - 1.0) * cos2th);

        return (a2 - 1.0) / (PI * log(a2) * den);
    }

    float D_GTR2(float alpha, float dotNH)
    {
        float a2 = alpha * alpha;
        float cos2th = dotNH * dotNH;
        float den = (1.0 + (a2 - 1.0) * cos2th);

        return a2 / (PI * den * den);
    }
  
以及各项异性的版本：

    float D_GTR2_aniso(float dotHX, float dotHY, float dotNH, float ax, float ay)
    {
            float deno = dotHX * dotHX / (ax * ax) + dotHY * dotHY / (ay * ay) + dotNH * dotNH;
            return 1.0 / (PI * ax * ay * deno * deno);
    }

### 菲涅尔项 Specular F：Schlick Fresnel
Disney 表示 Schlick Fresnel 近似已经足够精确，且比完整的菲涅尔方程简单得多; 而由于其他因素，Schlick Fresne 近似引入的误差明显小于其他因素产生的误差。Schlick Fresnel 近似公式如下：  

$$ F_{Schlick} = F_0 + (1 - F_0)(1 - cos \theta_d)^5 $$

以下为 Schlick Fresnel 的 Shader 实现代码：  

    // [Schlick 1994, "An Inexpensive BRDF Model for Physically-Based Rendering"]
    float3 F_Schlick(float Hdotl, float3 F0)
    {
        return F0 + (1 - F0) * pow(1 - Hdotl , 5.0);
    }

另外，Disney 在 SIGGRAPH 2015 上对此项进行了修订，提出在介质间相对 IOR 接近 1 时，Schlick 近似误差较大，这时可以直接用精确的菲涅尔方程：  

$$ F(\theta_i, \eta) = \begin{cases} \cfrac {1} {2} \left[ \left(\cfrac {cos \theta_i - \eta cos \theta_t} {cos \theta_i + \eta cos \theta_t} \right)^2 + \left( \cfrac {cos \theta_t - \eta cos \theta_i} {cos \theta_t + \eta cos \theta_i} \right)^2 \right], & \text{if $cos^2 \theta_t > 0$} \\ 1, & \text{otherwise} \end{cases}\\ $$

$\,\theta_i\,$、$\,\theta_t\,$ 为入射角和折射角。

### 几何项 Specular G：Smith - GGX
几何项方面，对于**主镜面波瓣 primary specular lobe**，Disney 参考了 Walter 的近似方法，使用 Smith GGX 导出的 G 项，并将粗糙度参数 α 进行重映射以减少光泽表面的极端增益，α 的值为 $\,(0.5 + roughness/2)^2\,$。从而使几何项的粗糙度变化更加平滑，更便于美术人员的使用。

以下为 Smith GGX 的几何项的表达式：  

$$ G(l,v,h) = G_{GGX}(l) G_{GGX}(v) $$  
$$ G_{GGX}(v) = \cfrac {2(n \cdot v)} {n \cdot v + \sqrt {\alpha^2 + (1 - \alpha^2)(n \cdot v)^2}} $$
$$ \alpha = (0.5 + roughness/2)^2 $$

另外，对于对清漆层进行处理的**次级波瓣 secondary lobe**，Disney 没有使用 Smith G 推导，而是直接使用固定粗糙度为 0.25 的 GGX 的 G 项，便可以得到合理且很好的视觉效果。

几何项的 Shader 实现代码如下：

    // Smith GGX G项，各项同性版本
    float smithG_GGX(float NdotV, float alphaG)
    {
        float a = alphaG * alphaG;
        float b = NdotV * NdotV;
        return 1 / (NdotV + sqrt(a + b - a * b));
    }

    // Smith GGX G项，各项异性版本
    // Derived G function for GGX
    float smithG_GGX_aniso(float dotVN, float dotVX, float dotVY, float ax, float ay)
    {
        return 1.0 / (dotVN + sqrt(pow(dotVX * ax, 2.0) + pow(dotVY * ay, 2.0) + pow(dotVN, 2.0)));
    }

同样，Disney 在 SIGGRAPH 2015 上也对 G 项进行了修订，他们基于 Heitz 的分析 [Heitz 2014]，淘汰了对于主镜面波瓣的 Smith G 粗糙度的特殊重映射，采用了 Heitz 各向异性的 G，下篇文章详细介绍 BRDF 模型时会对这次修订做更深入的探讨。

## Disney Principled BRDF 的实现代码
Disney 在 2012 年 Disney Principled BRDF 提出时，已经在 GitHub 上开源了 BRDF Explorer，以及 Disney Principled BRDF 的实现源码，其 GitHub 链接为：https://github.com/wdas/brdf/blob/main/src/brdfs/disney.brdf 

代码如下：  

    # variables go here...
    # [type] [name] [min val] [max val] [default val]
    ::begin parameters
    color baseColor .82 .67 .16
    float metallic 0 1 0
    float subsurface 0 1 0
    float specular 0 1 .5
    float roughness 0 1 .5
    float specularTint 0 1 0
    float anisotropic 0 1 0
    float sheen 0 1 0
    float sheenTint 0 1 .5
    float clearcoat 0 1 0
    float clearcoatGloss 0 1 1
    ::end parameters


    ::begin shader

    const float PI = 3.14159265358979323846;

    float sqr(float x) { return x*x; }

    float SchlickFresnel(float u)
    {
        float m = clamp(1-u, 0, 1);
        float m2 = m*m;
        return m2*m2*m; // pow(m,5)
    }

    float GTR1(float NdotH, float a)
    {
        if (a >= 1) return 1/PI;
        float a2 = a*a;
        float t = 1 + (a2-1)*NdotH*NdotH;
        return (a2-1) / (PI*log(a2)*t);
    }

    float GTR2(float NdotH, float a)
    {
        float a2 = a*a;
        float t = 1 + (a2-1)*NdotH*NdotH;
        return a2 / (PI * t*t);
    }

    float GTR2_aniso(float NdotH, float HdotX, float HdotY, float ax, float ay)
    {
        return 1 / (PI * ax*ay * sqr( sqr(HdotX/ax) + sqr(HdotY/ay) + NdotH*NdotH ));
    }

    float smithG_GGX(float NdotV, float alphaG)
    {
        float a = alphaG*alphaG;
        float b = NdotV*NdotV;
        return 1 / (NdotV + sqrt(a + b - a*b));
    }

    float smithG_GGX_aniso(float NdotV, float VdotX, float VdotY, float ax, float ay)
    {
        return 1 / (NdotV + sqrt( sqr(VdotX*ax) + sqr(VdotY*ay) + sqr(NdotV) ));
    }

    vec3 mon2lin(vec3 x)
    {
        return vec3(pow(x[0], 2.2), pow(x[1], 2.2), pow(x[2], 2.2));
    }


    vec3 BRDF( vec3 L, vec3 V, vec3 N, vec3 X, vec3 Y )
    {
        float NdotL = dot(N,L);
        float NdotV = dot(N,V);
        if (NdotL < 0 || NdotV < 0) return vec3(0);

        vec3 H = normalize(L+V);
        float NdotH = dot(N,H);
        float LdotH = dot(L,H);

        vec3 Cdlin = mon2lin(baseColor);
        float Cdlum = .3*Cdlin[0] + .6*Cdlin[1]  + .1*Cdlin[2]; // luminance approx.

        vec3 Ctint = Cdlum > 0 ? Cdlin/Cdlum : vec3(1); // normalize lum. to isolate hue+sat
        vec3 Cspec0 = mix(specular*.08*mix(vec3(1), Ctint, specularTint), Cdlin, metallic);
        vec3 Csheen = mix(vec3(1), Ctint, sheenTint);

        // Diffuse fresnel - go from 1 at normal incidence to .5 at grazing
        // and mix in diffuse retro-reflection based on roughness
        float FL = SchlickFresnel(NdotL), FV = SchlickFresnel(NdotV);
        float Fd90 = 0.5 + 2 * LdotH*LdotH * roughness;
        float Fd = mix(1.0, Fd90, FL) * mix(1.0, Fd90, FV);

        // Based on Hanrahan-Krueger brdf approximation of isotropic bssrdf
        // 1.25 scale is used to (roughly) preserve albedo
        // Fss90 used to "flatten" retroreflection based on roughness
        float Fss90 = LdotH*LdotH*roughness;
        float Fss = mix(1.0, Fss90, FL) * mix(1.0, Fss90, FV);
        float ss = 1.25 * (Fss * (1 / (NdotL + NdotV) - .5) + .5);

        // specular
        float aspect = sqrt(1-anisotropic*.9);
        float ax = max(.001, sqr(roughness)/aspect);
        float ay = max(.001, sqr(roughness)*aspect);
        float Ds = GTR2_aniso(NdotH, dot(H, X), dot(H, Y), ax, ay);
        float FH = SchlickFresnel(LdotH);
        vec3 Fs = mix(Cspec0, vec3(1), FH);
        float Gs;
        Gs  = smithG_GGX_aniso(NdotL, dot(L, X), dot(L, Y), ax, ay);
        Gs *= smithG_GGX_aniso(NdotV, dot(V, X), dot(V, Y), ax, ay);

        // sheen
        vec3 Fsheen = FH * sheen * Csheen;

        // clearcoat (ior = 1.5 -> F0 = 0.04)
        float Dr = GTR1(NdotH, mix(.1,.001,clearcoatGloss));
        float Fr = mix(.04, 1.0, FH);
        float Gr = smithG_GGX(NdotL, .25) * smithG_GGX(NdotV, .25);

        return ((1/PI) * mix(Fd, ss, subsurface)*Cdlin + Fsheen)
            * (1-metallic)
            + Gs*Fs*Ds + .25*clearcoat*Gr*Fr*Dr;
    }

    ::end shader


## Disney BSDF
Disney BSDF 天生适合离线渲染器使用，对游戏引擎和实时渲染的参考意义比较有限，这里只对它进行大致的介绍：  

在 2013 年上映的动画电影《冰雪奇缘》中，迪士尼继续沿用了之前开发的 Disney Principled BRDF 基于物理的着色系统，但对于折射和次表面散射等效果而言，需要与 BRDF 分开计算，且间接光照使用点云 point clouds 进行了近似。而从 2014 年的《超能陆战队（Big Hero 6）》开始，迪士尼开始采用路径追踪全局光照 path-traced global illumination 进行新电影的制作。所以，原本的 BRDF 模型已经无法满足需求，于是迪士尼在之前的 Disney Principled BRDF 的基础上进行了改进，新开发出了 Disney BSDF，并于 SIGGPRAPH 2015 上，通过 talk《Extending the Disney BRDF to a BSDF with Integrated Subsurface Scattering》正式提出。

Disney BRDF 模型本质上是金属和非金属的混合型模型，对于 Disney BSDF，Disney 仍然延续了之前的设计理念，采用了混合的方式并结合已有的 Disney BRDF 模型进行实现。如下图，Disney 新增了⼀个参数 specTrans 镜面反射透明度来控制 BRDF 和 BSDF的混合。基于 specTrans 完成混合后，再使用和 Disney BRDF 类似的方式，基于 metallic 再进行一次混合。即 **Disney BSDF 模型的本质是金属 BRDF、非金属 BRDF、与 Specular BSDF 三者的混合型模型**。

参数方面，Disney BSDF 按普通表面和薄表面各有不同：  
①对于普通表面，Disney BSDF 在 Disney BRDF 的基础上新增 specTrans 镜面反射透明度和 scatterDistance 散射距离 两个参数，共 12 个。  
②对于薄表面 Thin-surface，Disney BSDF 在 Disney BRDF 的基础上新增 specTrans 镜面反射透明度、scatterDistance 散射距离和 flatness 平坦度三个参数，共 13 个。

---

除了新增的 Specular BSDF 模型，Disney 还提出了新的次表面散射模型，以及针对薄表面的折射处理，可以总结如下：  
①在 Disney BRDF 中加入次表面散射模型。具体思路是首先将漫射波瓣重构为两部分：方向性的微表面效应 microsurface effect，主要为逆反射 retroreflection；非方向性的次表面效应 subsurface effect，即 Lambertian。然后，用散射模型 diffusion model 或体积散射模型 volumetric scattering model 替换漫反射波瓣中的 Lambert 部分。这样，便能保留微表面效应 microsurface effect，让散射模型在散射距离较小时收敛到与漫反射 BRDF 相同的结果。  
②提出基于两个指数项总和的次表面漫射 Subsurface diffusion 模拟模型。Disney 通过蒙特卡洛模拟 Monte Carlo simulation，观察到对于典型的散射参数，包括单次散射的扩散剖面 diffusion profile，使用两个指数项的总和 a sum of two exponentials 便可以很好地进行模拟，且得到了比偶极子剖面 dipole diffusion 更好的渲染结果。  
③薄表面 BSDF Thin-surface BSDF。对于薄的半透明表⾯，Disney 选择在单个着色点处模拟入射和出射散射事件，作为镜面反射和漫反射传输的组合，由 specTrans 和 diffTrans 参数控制，并用各向同性的波瓣近似薄表面漫反射传输。