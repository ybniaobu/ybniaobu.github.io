---
title: Custom Better PBR in Unity
date: 2024-10-22 11:13:12
categories: 
  - [图形学]
  - [unity, pipeline]
tags:
  - 图形学
  - 游戏开发
  - unity
top_img: /images/black.jpg
cover: https://s2.loli.net/2024/10/22/x6Xp3JjS4k5PO2W.gif
mathjax: true
description: 
---

> 本文章所阐述的 PBR Shader 仍然是在 URP 下的自定义 Shader，使用的仍然是最简单的前向渲染，为未来实现自己的自定义渲染管线 SRP 打下一定的基础。本篇文章创建时期，该 Shader 只包含最基础的 BRDF 标准模型，以后会增加 Anisotropic、Clearcoat、Cloth、Subsurface 等模型，我放在我的 Github 库里：https://github.com/ybniaobu/Unity_YPipeline ，最终在这个库中会实现自己的自定义渲染管线 YPipeline。
>  
> 该 PBR Shader 主要参考了著名的 LearnOpenGL 教程：https://learnopengl-cn.github.io/ 或 https://learnopengl.com/ ，以及谷歌的 Filament PBR 白皮书：https://google.github.io/filament/Filament.md.html#materialsystem/diffusebrdf 。

# Standard Shader 整体框架
顶点着色器不多说了，片元着色器整体框架如下：  
**1\. 初始部分**
$$\text{①声明 RenderingEquationContent 结构体}  \rightarrow \text{②声明并初始化材质及着色模型相关参数 InitializeStandardPBRParams} $$

**2\. IBL 部分**
$$ \text{③计算 RenderingEquationContent 里的 indirectLight (IBL) 项} $$

**3\. 主光源部分**
$$ \text{④声明并初始化主光源相关参数 InitializeMainLightParams} \rightarrow \text{⑤声明并初始化主光源 BRDF 计算所需参数 InitializeBRDFParams} $$ 

$$ \rightarrow \text{⑥计算 RenderingEquationContent 里的 directMainLight 项} $$

**3\. 其他光源部分**
$$ \text{⑦声明并初始化其他光源相关参数 InitializeAdditionalLightParams} \rightarrow \text{⑧声明并初始化其他光源 BRDF 计算所需参数 InitializeBRDFParams} $$ 

$$ \rightarrow \text{⑨计算 RenderingEquationContent 里的 directAdditionalLight 项} $$

每个步骤说明：  
**①**首先整个 Shader 的目的就是计算**渲染方程 The Rendering Equation**，即：L = Emission + Direct light diffuse term + Direct light specular term + Indirect light diffuse term + Indirect light specular term。由于游戏内需求的灯光不止一盏，故将 Direct light 分为 Main light 和 Additional light（暂时按 Unity 的 Main light 和 Additional light 的分法来，日后再修改）。故渲染方程可写为：L = Emission + Main Light + Additional Light + Indirect light (IBL)。

我把这些内容放置在了 `RenderingEquationLibrary.hlsl` 里的结构体 `RenderingEquationContent`：

    struct RenderingEquationContent
    {
        float3 directMainLight;
        float3 directAdditionalLight;
        float3 indirectLight;
    };

故第一步就是声明该结构体变量。暂时没在该结构体里增加 Emission 项，因为自发光的物体的处理比较特殊，日后自定义渲染管线时专门处理，暂时不在 Standard Shader 里增加自发光功能。

**②**接下来就是获取渲染方程计算所需的参数，首先是材质及着色模型相关的参数，这些参数不包括跟光源有关的参数，比如光源方向 $\,l\,$、半角向量 $\,h\,$ 等等。由于不同的着色模型所需参数可能不同，故针对不同着色模型有不同的结构体，我都放在了 `BRDFModelLibrary.hlsl` 里了，比如标准模型所需的参数结构体 `StandardPBRParams`：  

    struct StandardPBRParams
    {
        float3 albedo;
        float roughness;
        float metallic;
        float3 F0;
        float3 N; // L、H is related to the light，see XXXLightsLibrary.
        float3 V;
        float NoV;
    };

接下来就是根据材质属性、贴图等获取这些参数了，我都放在了函数 `InitializeStandardPBRParams(Varyings IN, out StandardPBRParams standardPBRParams)` 里了，因为我觉得这个函数会根据不同 Shader 的不同材质属性要求而有所不同，所以我没有将其封装在 ShaderLibrary 文件里，而是写在了 Shader 的 Pass 代码的 HLSL 文件中。

**③**然后就是计算 IBL 项，至于为什么先计算 IBL 项，是因为 IBL 的计算跟其他光源无关，我们只需要用法线 $\,n\,$ 或视角反射方向 $\,r\,$ 对预过滤的环境贴图或者球谐函数采样，以及用 roughness 和 NoV 对 Environment Lut 采样即可，这些参数都是 `StandardPBRParams` 里都计算好的。还有个原因就是若在标准模型里考虑 multiscattering 的话，我们需要用 Environment Lut 计算一个 energy compensation 项，以便后面直接光的计算使用，这个之后会在 IBL 里详细说明。  

**④、⑦**主光源流程和其他光源很像，就放在一起说了。首先就是初始化不同光源各自的参数，参数结构体 `LightParams` 放在了 `PunctualLightsLibrary.hlsl` 里（目前只实现了 punctual lights，日后还需添加 photometric lights、area lights 等），如下：  

    struct LightParams
    {
        float3 color;
        float4 positionWS;
        float3 L;
        float3 H;
        float distanceAttenuation;
        float angleAttenuation;
        float shadowAttenuation;
        uint layerMask;
    };

初始化这些参数的函数即 `InitializeMainLightParams()` 以及 `InitializeAdditionalLightParams()`。

**⑤、⑧**我们得到了 4 个主要方向 $\,n\,$、$\,v\,$、$\,l\,$、$\,h\,$ 后，就可以计算 BRDF 各项所需的参数了，即 `BRDFParams` 结构体：

    struct BRDFParams
    {
        // NoV is in BRDFModelParams
        float NoL;
        float NoH;
        float LoH;
        float VoH;
    };

计算这些参数的函数即 `InitializeBRDFParams()`，都写在 `BRDFTermsLibrary.hlsl` 文件中。

**⑥、⑨**最后即计算 BRDF 中的 diffuse term 和 specular term，根据不同的着色模型会有不同的计算函数，比如标准模型的 `StandardPBR()` 函数，我都放在了 `BRDFModelLibrary.hlsl` 文件中。注意：我将 NoL 乘在了 BRDF 里，有些人会将它乘在 Lighting 的 Attenuation 项里。

# 参数说明
## Roughness
### remapping
关于 roughness，我们在各种文章资料里总是能看到 Roughness, Linear Roughness, Perceptual Roughness, Alpha, Alpha2 等等，经常会傻傻分不清，其实我在我之前的博客文章里也分得不是很清楚。故在这里详细说明一下：  

首先大部分资料里的做法基本上就是：开发者控制的材质参数为 Linear Roughness/Perceptual Roughness，即我们调节的 0 到 1 的值。那么

$$ \alpha(Alpha) = Roughness = (\text{Linear Roughness or Perceptual Roughness})^2 $$
$$ \alpha2(Alpha2) = \alpha^2(Alpha^2) = Roughness^2 = (\text{Linear Roughness or Perceptual Roughness})^4 $$
 
之所以要搞这个 roughness remapping，是因为不 remapping 的话，在粗糙度低时，粗糙度变化会随参数响应过快，而粗糙度高时，粗糙度变化会随参数响应过慢，如下图（来源于寒霜的《Moving Frostbite to Physically Based Rendering》）：

<div  align="center">  
<img src="https://s2.loli.net/2024/10/23/Z3ctsd7BYPuivLb.png" width = "70%" height = "70%" alt="图1 - roughness remapping"/>
</div>

而我的方案里，就不区分 Linear Roughness 和 Roughness 了，开发者控制的材质参数统一为 roughness，传递给 BRDF 各项的函数的参数都统一为 roughness，只不过我的：  

$$ a = \alpha = roughness^2 $$
$$ a2 = a^2 = roughness^4 $$

### clamping
一般在手机平台上会使用 half 精度，即 16-bit floating-point (float16)。在 PC 或主机端一般使用 float, Single-precision floating-point format (float32)。至于 double，目前主流的游戏显卡对其的支持都相对较差，双精度计算单元只有单精度计算单元的 1/32，有兴趣可以去查阅显卡相关资料。

为了防止四次方超出精度范围，从而导致除以 0 的问题，我们需要给 roughness 设置一个最小值，这样做还有一个好处就是可以保证存在一个较小的高光波瓣，即使 roughness 设置为 0（默认情况下，roughness 为 0 时，高光波瓣会小到消失）。

对于 half，即 $\,2^{-14}\,$（$\,6.10352 \times 10 ^ {-5}\,$），为了防止 $\,\cfrac {1}{roughness^4}\,$ 小于该数，那么 roughness 的最小值应该设置为 0.089，这样计算出来为 $\,6.27422 \times 10 ^ {-5}\,$。对于 float，即 $\,2^{-126}\,$（$\,1.17549 \times 10 ^ {-38}\,$），那么 roughness 的最小值可以被设置为一个很小的数字。

我的方案是将 roughness 的最小值设置为了 0.02。但要注意：最好在计算 direct light 的 BRDF 时，以及在预过滤环境贴图以及预积分 BRDF 时做 clamping。最好不要直接在获取材质属性的时候做 clamping，因为这样 roughness 为 0 时，我们根据 roughness 采样贴图时可能会得到不是我们想要的结果。

## Reflectance
### F0
接下来是电介质的反射率的问题，我参考了寒霜和 Filament 的 remapping 方法。我在 Shader 的材质属性里添加了名为 Dielectrics Specular Intensity 的参数（寒霜和 Filament 写为 Reflectance），它是用于控制电解质的 F0 的，并且电解质的 F0 都是非彩色的，公式如下：  

$$ F_0 = 0.16 \cdot Reflectance^2 $$

这样 F0 的范围就可以覆盖几乎所有常见的电解质表面了，一般电解质 F0 在 0.02 ~ 0.08 之间，一些钻石会在 0.16 左右。而 Reflectance 参数的默认值设为 0.5，这样 F0 就默认为最常见的 0.04。

金属的 F0 则由我们设置的颜色控制，即 Albedo。那么可以用金属度进行插值来控制 F0 的颜色：  

    float3 F0 = lerp(_Specular * _Specular * float3(0.16, 0.16, 0.16), albedo, metallic);

### 能量守恒
<font color=#FF0000> TODO：$\,(1 - F)(1 - Metallic)\,$ 的问题</font>

# BRDF 各项说明
## Direct Diffuse
选用的是寒霜的 Renormalized Disney diffuse，代码和效果如下：

    float3 Fd_RenormalizedBurley_Disney(float NoV, float NoL, float LoH, float roughness, float3 diffuseColor)
    {
        float energyBias = lerp(0, 0.5, roughness);
        float energyFactor = lerp(1.0, 1.0 / 1.51, roughness);
        float fd90 = energyBias + 2.0 * LoH * LoH * roughness;
        float FdL = F_Schlick(fd90, 1.0, NoL);
        float FdV = F_Schlick(fd90, 1.0, NoV);
        return FdV * FdL * energyFactor * INV_PI * diffuseColor;
    }

<div  align="center">  
<img src="https://s2.loli.net/2024/10/25/srlfJyPN3WiUoFB.jpg" width = "50%" height = "50%" alt="图2 - Renormalized Disney diffuse（从左到右 roughness 分别为 0，0.5，1）"/>
</div>

和 Disney diffuse 一样，在粗糙度为 0 时有黑边，在粗糙度为 1 时有白边。只不过寒霜做了能量守恒，Disney 的黑边更淡一点，白边更亮一点，如下图：

<div  align="center">  
<img src="https://s2.loli.net/2024/10/25/VQeMXPtGs9a7Hup.jpg" width = "50%" height = "50%" alt="图3 - Disney(Burley) diffuse"/>
</div>

## Direct Specular
菲涅尔 Fresnel 项，选用的就是 Schlick 的 Fresnel 近似式；法线分布函数 NDF 项，选用的是 GGX (Trowbridge-Reitz) 分布；可见性 Visibility 项，选用的是 GGX-Smith Correlated Joint。

    float3 F_Schlick(float f90, float3 f0, float VoH)
    {
        return f0 + (float3(f90, f90, f90) - f0) * pow(1.0 - VoH, 5.0);
    }

    float D_GGX(float NoH, float roughness)
    {
        float a2 = pow(roughness, 4.0);
        float d = (NoH * a2 - NoH) * NoH + 1.0;
        return a2 / (PI * d * d);
    }

    float V_SmithGGXCorrelated(float NoV, float NoL, float roughness)
    {
        float a2 = pow(roughness, 4.0);
        float V_SmithL = NoV * sqrt((-NoL * a2 + NoL) * NoL + a2);
        float V_SmithV = NoL * sqrt((-NoV * a2 + NoV) * NoV + a2);
        return 0.5 / (V_SmithL + V_SmithV);
    }

我采用了 $\,(1 - Metallic)\,$ 的方式来对漫反射和镜面反射的贡献进行平衡，代码如下：

    float3 StandardPBR(BRDFParams BRDFParams, StandardPBRParams standardPBRParams)
    {
        float roughness = clamp(standardPBRParams.roughness, 0.02, 1.0); //make sure there is a tiny specular lobe when roughness is zero
        float3 diffuse = Fd_RenormalizedBurley_Disney(standardPBRParams.NoV, BRDFParams.NoL, BRDFParams.LoH, roughness, standardPBRParams.albedo);
        
        float D = D_GGX(BRDFParams.NoH, roughness);
        float V = V_SmithGGXCorrelated(standardPBRParams.NoV, BRDFParams.NoL, roughness);
        float3 F = F_Schlick(1, standardPBRParams.F0, BRDFParams.VoH);
        float3 specular = D * V * F;
        
        return (diffuse * (float3(1.0, 1.0, 1.0) - F) * (1 - standardPBRParams.metallic) + specular) * BRDFParams.NoL;
    }

Direct Diffuse 和 Direct Specular 合并的 Direct Light 效果如下：  

<div  align="center">  
<img src="https://s2.loli.net/2024/10/29/OwhNBezrQA4WCvF.jpg" width = "70%" height = "70%" alt="图4 - Direct Light（正交投影）"/>
</div>

但是按照上面这样处理，高光颜色会有点小瑕疵，特别是在 albedo 颜色是彩色，且 RGB 三通道值都不一样的时候。我们只看高光部分，金属度为 1 时，如下图所示：  

<div  align="center">  
<img src="https://s2.loli.net/2024/10/25/zADWCMBqyEnjX4k.png" width = "40%" height = "40%" alt="图5 - 高光分层（粗糙度为 0.25）"/>
</div>

我将 RGB 分别设置为了 1，0.6，0.3。可以看到高光分为了三层，最里面为白色（红绿蓝组合），中间层为黄色（红绿组合），最外层就是设置的 RGB 颜色。之所以会有这个现象是因为计算 F 项时，代码是对 RGB 三个值分别计算的，值的不同大小会影响到波瓣大小，于是就出现了分层现象。

> 我观察了 Blender 的 Eevee 实时渲染器，发现没有这个现象。但是！！！！我后来发现是 Blender 自动开了 Tone Mapping 导致没有这个现象，关掉也有这个现象，我吐了，浪费了我不少时间找解决方案。然后我就开了 ACES Tone Mapping 看了下，发现这个现象确实得到了很好的修复。

## Multiscattering Specular
我们使用的 Cook-Torrance BRDF 模型只考虑的光线的一次弹射，这会导致在高粗糙度下的能量损失。如下图所示，在单次散射模型中，反弹到其他微平面的光线会因为遮蔽阴影函数而被放弃，而在多次散射模型中，光线仍然有可能逃离微平面区域，进入观察者视野：  

<div  align="center">  
<img src="https://s2.loli.net/2024/10/27/TaS562fh14YRwV8.png" width = "60%" height = "60%" alt="图6 - 单次散射和多次散射"/>
</div>

单次散射模型能量不守恒，表现出来就是粗糙度越高整体越暗。为了改善这一现象，我们可以增加一个 **energy compensation** 项作为另一个 BRDF 波瓣（Multiple-scattering BRDF 对于实时渲染来说不太适用，所以使用了这个妥协的方法），公式如下：  

$$ f_r(l, v) = f_{ss}(l, v) + f_{ms}(l, v) $$

其中 ss 即 single scattering，ms 即新添加的 multi scattering 波瓣。

$$ f_{ms}(l, v) = f_0 \cfrac {1 - r}{r} f_{ss}(l, v) $$

即：  

$$ f_r(l, v) = f_{ss}(l, v) + f_0 \left( \cfrac {1}{r} - 1 \right) f_{ss}(l, v) $$

其中 r 的定义如下：  

$$ r = \int_{\Omega} D(\omega_h) V(\omega_i, \omega_o) n \cdot \omega_i d \omega_i $$

这个 r 我们可以从 Environment Lut 中获取，只要我们略微改变一下 Environment Lut 的生成代码，详见 IBL Specular 部分。energy compensation 的实现代码和效果如下：  

    float3 energyCompensation = 1.0 + F0 * (1.0 / envBRDF.x - 1.0);
    Fr *= energyCompensation;

<div  align="center">  
<img src="https://s2.loli.net/2024/10/29/TRo6A29b8yv5FIQ.jpg" width = "70%" height = "70%" alt="图7 - energy compensation"/>
</div>

高粗糙度时确实亮了，但是效果有点不连续，看起来很难受，特别是金属为 1 时比上面都亮。我研究了一下原因，是因为我选用的 Diffuse 模型和 energy compensation 不搭所导致的不连续变化，Diffuse 选用 Lambert 模型就变得特别舒服，效果如下：  

<div  align="center">  
<img src="https://s2.loli.net/2024/10/29/46FTCudlwkGDty1.jpg" width = "70%" height = "70%" alt="图8 - 选用 Lambert 模型后的 energy compensation 效果"/>
</div>

我的解决方案是修改 energyCompensation 项的变化率，让其更适配 Renormalized Disney diffuse 模型，试了试，发现开根号或者直接除以 2 都还行：  

    float3 energyCompensation = 1.0 + sqrt(F0 * (1.0 / envBRDF.x - 1.0));
    float3 energyCompensation = 1.0 + F0 * (1.0 / envBRDF.x - 1.0) / 2;

除以 2 的效果如下：  

<div  align="center">  
<img src="https://s2.loli.net/2024/10/29/afLoCU7sVjWPpdw.jpg" width = "70%" height = "70%" alt="图9 - Renormalized Disney diffuse + energy compensation 除以 2 后的效果"/>
</div>

## IBL Diffuse
在之前的 IBL 文章中，漫反射项是假定了是 Lambert 模型，如下：  

$$ L_o(p, \omega_o) = k_d^* \cfrac {c} {\pi} \int_{\Omega} L_i(p, \omega_i) n \cdot \omega_i d\omega_i $$

我们将 $\,\cfrac {c} {\pi}\,$ 移出是因为它是常数项，现在我们使用的漫反射是 Renormalized Disney diffuse，那么直接移出就相对来说没这么准确。实际上，我们也可以使用类似于 Spilt sum approximation 的方法，对积分进行拆分并预计算从而提高 IBL 漫反射项的效果，我们只需要把预积分的结果放在 Environment Lut 的 b 通道即可。

但是我在实践中发现不同的方法效果会有些许差距，我先从寒霜方案讲起，最后到我自己的方案。

### 寒霜方案
首先是寒霜的文章中的方案：https://media.contentapi.ea.com/content/dam/eacom/frostbite/files/course-notes-moving-frostbite-to-pbr-v32.pdf 。寒霜使用了余弦分布的重要性采样，概率分布函数 PDF 为 $\,cos \theta / \pi \,$。拆分步骤如下：  

$$ \begin{align*} L_o(p, \omega_o) &= \int_{\Omega} f_d L_i(p, \omega_i) n \cdot \omega_i d\omega_i \\
&= \cfrac {1}{N} \sum_{i = 1}^N \cfrac {f_d L_i n \cdot \omega_i} {\rho(\omega_i)} \\ &= \cfrac {1}{N} \sum_{i = 1}^N \cfrac {f_d L_i n \cdot \omega_i} {n \cdot \omega_i / \pi} \\ &= \cfrac {1}{N} \sum_{i = 1}^N \pi f_d L_i \\ & \approx \cfrac {\pi}{N} \sum_{i = 1}^N f_d \times \cfrac {1}{N} \sum_{i = 1}^N L_i \end{align*} $$

预积分代码如下： 

``` C
float4 CosineSampleHemisphere(float2 xi) // Left-handed Spherical and Cartesian Coordinate
{
    float phi = PI * (2.0 * xi.x - 1.0);
    float cosTheta = sqrt(1 - xi.y);
    float sinTheta = sqrt(1 - cosTheta * cosTheta);

    float3 N = float3(sinTheta * cos(phi), cosTheta, sinTheta * sin(phi));
    float PDF = cosTheta * INV_PI;
    return float4(N, PDF);
}

float PreintegrateDiffuse_RenormalizedBurley(float roughness, float NoV)
{
    float3 V = float3(sqrt(1.0 - NoV * NoV), NoV, 0.0);

    float fd = 0.0;
    const uint sampleNumber = 2048;

    for (uint i = 0; i < sampleNumber; i++)
    {
        float2 xi = Hammersley(i, sampleNumber);
        float3 L = CosineSampleHemisphere(xi).xyz;
        float3 H = normalize(L + V);

        float NoL = saturate(L.y);
        float LoH = saturate(dot(L, H));

        if (NoL > 0)
        {
            float diffuse = Fd_RenormalizedBurley_Disney_NoPI(NoV, NoL, LoH, roughness);
            fd += diffuse;
        }
    }
        
    return fd / sampleNumber;
}
```

上面假设 N 就是 (0, 1, 0)，所以不需要将 L 从切线空间转换到世界坐标了，都是一样的。因为 Renormalized Disney diffuse 也要乘上 Albedo 除以 $\,\pi\,$ 的，使用 Fd_RenormalizedBurley_Disney_NoPI 函数，这样后面就不用再乘上 $\,\pi\,$ 了。

寒霜是将后半项 L 项存储到了 irradinace cubemap 里，代码我不就摘抄了，同样 CosineSampleHemisphere。但是我没预计算这部分，我直接使用了 SH 项，Unity 计算的 SH 项的等效蒙特卡洛积分如下：  

$$ \cfrac {1} {\pi} \int_{\Omega} L_i(p, \omega_i) n \cdot \omega_i d\omega_i = \cfrac {1} {\pi} \cdot \cfrac {1} {N} \sum_{i = 1}^N \cfrac {L_i n \cdot \omega_i} {\rho(\omega_i)} = \cfrac {1} {\pi} \cdot \cfrac {1}{N} \sum_{i = 1}^N \cfrac { L_i n \cdot \omega_i} {n \cdot \omega_i / \pi} = \cfrac {1}{N} \sum_{i = 1}^N L_i $$

所以 SH 项和寒霜 L 项是等效的，我们直接用就行，那么只看 IBL Diffuse 项的效果如下： 

<div  align="center">  
<img src="https://s2.loli.net/2024/10/29/H39dWrkhQZUT8G2.jpg" width = "70%" height = "70%" alt="图10 - 寒霜预积分后的 IBL diffuse 效果"/>
</div>

这个方案的问题在于粗糙度低和高的亮度差异有点大，但是粗糙度为 0 时的亮度，和直接采样 SH 的亮度是十分接近的。

### 我的方案
因为一开始我没有看到寒霜的文章里的上述方案，我当时仿照 Spilt sum approximation 自己计算了一个，推导步骤如下：  

$$ \begin{align*} L_o(p, \omega_o) &= \int_{\Omega} f_d L_i(p, \omega_i) n \cdot \omega_i d\omega_i \\
&=  \cfrac {\int_{\Omega} f_d L_i(p, \omega_i) n \cdot \omega_i d\omega_i} {\int_{\Omega} f_d d\omega_i} \int_{\Omega} f_d d\omega_i \end{align*} $$

前半部分即 L 项，后半部分即预积分项，先看前半部分：  

$$ L = \cfrac {\int_{\Omega} f_d L_i(p, \omega_i) n \cdot \omega_i d\omega_i} {\int_{\Omega} f_d d\omega_i} = \cfrac {\frac{1}{N} \sum_{i=1}^N \cfrac {f_d L_i n \cdot \omega_i} {\rho(\omega_i)}} {\frac{1}{N} \sum_{i=1}^N \cfrac {f_d} {\rho(\omega_i)}} $$

我的 PDF 就是逆变换半球采样的 $\,\cfrac {1} {2\pi}\,$。是常数，可以直接提出来，分子分母约掉：  

$$ L = \cfrac {\frac{1}{N} \sum_{i=1}^N f_d L_i n \cdot \omega_i} {\frac{1}{N} \sum_{i=1}^N f_d} $$

我这里直接假设 $\,l = n = v\,$，这样 $\,f_d\,$ 就退化为了 Lambert，是常数项，也可以提出来约掉：  

$$ L = \frac{1}{N} \sum_{i=1}^N L_i n \cdot \omega_i $$

这样整个公式就等于：  

$$ L_o(p, \omega_o) = \frac{1}{N} \sum_{i=1}^N L_i n \cdot \omega_i \times \frac{2 \pi}{N} \sum_{i=1}^N f_d $$

我将后面的 2 挪到了前面，这样 L 项就和 SH 项的等效蒙特卡洛积分一样了：  

$$ \cfrac {1} {\pi} \int_{\Omega} L_i(p, \omega_i) n \cdot \omega_i d\omega_i = \cfrac {1} {\pi} \cdot \cfrac {1} {N} \sum_{i = 1}^N \cfrac {L_i n \cdot \omega_i} {\rho(\omega_i)} = \cfrac {1} {\pi} \cdot \cfrac {1}{N} \sum_{i = 1}^N \cfrac { L_i n \cdot \omega_i} {1 / (2\pi)} = \cfrac {2}{N} \sum_{i = 1}^N L_i n \cdot \omega_i $$

这样预积分代码就如下： 

``` C
float4 InverseSampleHemisphere(float2 xi) // Left-handed Spherical and Cartesian Coordinate
{
    float phi = PI * (2.0 * xi.x - 1.0);
    float cosTheta = 1 - xi.y;
    float sinTheta = sqrt(1 - cosTheta * cosTheta);

    float3 N = float3(sinTheta * cos(phi), cosTheta, sinTheta * sin(phi));
    float PDF = INV_TWO_PI;
    return float4(N, PDF);
}

float PreintegrateDiffuse_RenormalizedBurley(float roughness, float NoV)
{
    float3 V = float3(sqrt(1.0 - NoV * NoV), NoV, 0.0);

    float fd = 0.0;
    const uint sampleNumber = 2048;

    for (uint i = 0; i < sampleNumber; i++)
    {
        float2 xi = Hammersley(i, sampleNumber);
        float3 L = InverseSampleHemisphere(xi).xyz;
        float3 H = normalize(L + V);

        float NoL = saturate(L.y);
        float LoH = saturate(dot(L, H));

        if (NoL > 0)
        {
            float diffuse = Fd_RenormalizedBurley_Disney_NoPI(NoV, NoL, LoH, roughness);
            fd += diffuse;
        }
    }
    
    return fd / sampleNumber;
}
```

我的方案只看 IBL Diffuse 项的效果如下：

<div  align="center">  
<img src="https://s2.loli.net/2024/10/29/wL6UrzDG9xbKaRP.jpg" width = "70%" height = "70%" alt="图11 - 我的预积分方案的 IBL diffuse 效果"/>
</div>

可以看到我的方案亮度变化没有这么大，且粗糙度为 0 时比寒霜暗，粗糙度为 1 时比寒霜亮，变化更加均匀一点，我猜测是我的退化至 Lambert 的假设导致的变化没这么大。我的方案问题是粗糙度为 0 最亮时，仍然比直接采样 SH 暗一点。我更喜欢我这个方案，所以选择了自己的方案。

## IBL Specular
这部分就是之前 IBL 文章里 Epic Games 的 Spilt sum approximation 方案。

### Prefilter Environment Map
预过滤的代码如下：  

``` C
float3 PrefilterEnvMap_GGX(TEXTURECUBE(envMap), SAMPLER(envMapSampler), uint sampleNumber, float resolutionPerFace, float roughness, float3 R)
{
    float3 N = R;
    float3 V = R;

    float3 prefilteredColor = 0;
    float totalWeight = 0;
    
    for( uint i = 0; i < sampleNumber; i++ )
    {
        float2 xi = Hammersley(i, sampleNumber);
        float4 HandPDF = ImportanceSampleGGX(xi, roughness);
        float3 H = TangentCoordToWorldCoord(HandPDF.xyz, N);
        float PDF = HandPDF.w;
        float3 L = 2 * dot( V, H ) * H - V;

        float NoL = saturate(dot(N, L));
        
        if (NoL > 0)
        {
            // Reduce artifacts due to high frequency details by sampling a mip level of the environment map based on the integral's PDF and the roughness
            // resolutionPerFace is the resolution of source cubemap (per face)
            float saTexel  = FOUR_PI / (6.0 * resolutionPerFace * resolutionPerFace);
            float saSample = 1.0 / (float(sampleNumber) * PDF + 0.0001);
            float mipLevel = roughness == 0.0 ? 0.0 : 0.5 * log2(saSample / saTexel);
            
            prefilteredColor += envMap.SampleLevel(envMapSampler, L, mipLevel * 1.25).rgb * NoL; //1.25 is a magic/empirical number
            totalWeight += NoL;
        }
    }
    
    return prefilteredColor / totalWeight;
}
```

首先 Epic Games 的文章中的 PrefilterEnvMap 的代码在高粗糙度和采样不足的情况下会出现 Sampling Artifacts，特别是 HDR 的环境贴图，因为 HDR 总是会包含较多的高频信息。一个可行的方法就是采样预过滤后的环境贴图，也就是前置滤波，从而达到一个反走样的功能。具体的操作就是采样 Cubemap 的 mipmaps，因为 mipmaps 就是预过滤后的 Cubemap，我们在 Unity 中导入环境贴图后，最好选用**三线性过滤 Trilinear filtering**。而决定要采样的 mipmap 的 LOD 的公式如下：  

$$ lod = log_4 \left( \cfrac{\Omega_s} {\Omega_p} \right) = 0.5 \times log_2 \left( \cfrac{\Omega_s} {\Omega_p} \right) $$
$$ \Omega_s = \cfrac {1} {N \cdot PDF} $$
$$ \Omega_p = \cfrac {4 \pi} {6 \cdot width \cdot height} $$

其中 $\,\Omega_s\,$ 是每个采样点对应的立体角，$\,\Omega_p\,$ 是 Cubemap 的每个纹素。但是实际操作中，这样处理对于高分辨率的 Cubemap 仍然不够有效，还是会有 Artifacts 问题，所以我在采样的时候对 mipLevel 乘以了 1.25，这个数字不是固定的，根据不同情况要测试过再使用，我乘以 1.25 之后就几乎没有 Artifacts 问题了。或者也可以直接在 mipLevel 上加 1。

---

在预过滤 Cubemap 的时候遇到了很多实践上的问题，我觉得有必要阐述一遍，说实话这里浪费了我不少时间。

**①**本来我是想用 Computer Shader 对 Cubemap 进行预过滤的，但是 HLSL 不存在 RWTextureCube，虽然可以对 TextureCube 进行采样。所以我们只能使用 RWTexture2DArray。但是因为我们要根据不同的粗糙度，存储到不同的 Mipmap 中，假设存储 6 个粗糙度，就需要 6 个 RWTexture2DArray，每个 Texture2DArray 需要有六个面。将 Texture2DArray 传递回 CPU 后，还得在 CPU 里将每个 Texture2DArray 的每个面复制给 Cubemap 的每个 mipmap 的每个面。说实话不是不能写，但真的太繁琐了，所以我没这么做。以后碰到需求了再写。

**②**我后来就想到了 Unity 的 Camera.RenderToCubemap API，并且直接在 Skybox 的 Shader 里进行预过滤，我调用了多次 Camera.RenderToCubemap 分别将不同粗糙度的预过滤 Skybox 渲染到了多个不同分辨率的 Cubemap 里（因为 Camera.RenderToCubemap 无法直接渲染到 mipmap 里），最后再将不同分辨率的 Cubemap 复制到一个 Cubemap 的不同 mipmap 里。最后用 AssetDatabase.CreateAsset 创建 Cubemap 资产，在创建前还可以用 EditorUtility.CompressCubemapTexture 对 Cubemap 进行压缩。但是输出 Cubemap 后我发现颜色暗了点，我就以为 RenderToCubemap API 自动进行了 gamma 0.45 处理，所以在 Skybox 的 Shader 进行了 gamma 2.2 处理。但是就在我以为大功告成的时候，在使用计算出来的 Cubemap 后发现，大于 1 的颜色值都变为了 1，导致 IBL 的高光亮度不对，特别是开了 Bloom 后就能很快发现这个问题。所以 RenderToCubemap API 是进行了 Tonemapping 处理，将 HDR 映射到了 LDR，再进行了 gamma 0.45 处理。问题就在于，若直接在 Shader 里做逆向 Tonemapping，有些值会超出精度范围，我试了一下，确实会。所以只能在 CPU 里进行逆向 Tonemapping 处理了，这就更坑了，因为 CPU 逐像素操作慢得可怜，所以最后我放弃了。

**③**最后我就暂时先使用 Unity 自带的 Cubemap 的 specular 卷积功能，但是它是基于 Phong 波瓣的，不得不说 Unity 最近几年确实越来越偏离主业了，连个 GGX 波瓣都不更新，虽然从效果而言 Phong 波瓣和 GGX 波瓣区别确实也不大。

**④**有些 HDR 贴图会带有太阳的直接光照，理论上来说应该剔除掉太阳的直接光照的影响，Blender 就有这方面的功能，Blender 会把 HDR 中最大的光照贡献转换为太阳直接光。具体实践以后遇到再说。

### Environment Lut
原来的预积分代码如下： 

``` C
float2 PreintegrateSpecular_SmithGGXCorrelated(float roughness, float NoV)
{
    float3 V = float3(sqrt(1.0 - NoV * NoV), NoV, 0.0);
    float3 N = float3(0.0, 1.0, 0.0);

    float r = 0.0;
    float g = 0.0;
    const uint sampleNumber = 2048;

    for (uint i = 0; i < sampleNumber; i++)
    {
        float2 xi = Hammersley(i, sampleNumber);
        float3 H = ImportanceSampleGGX(xi, roughness, N);
        float3 L = 2.0 * dot(V, H) * H - V;

        float NoL = saturate(L.y);
        float NoH = saturate(H.y);
        float VoH = saturate(dot(V, H));

        if (NoL > 0)
        {
            float V = V_SmithGGXCorrelated(NoV, NoL, roughness);
            float G = V * 4 * NoL * NoV;
            float G_Vis = G * VoH / (NoH * NoV);
            float Fc = pow(1.0 - VoH, 5);
            
            r += (1.0 - Fc) * G_Vis;
            g += Fc * G_Vis;
        }
    }

    return float2(r, g) / sampleNumber;
}
```

但是因为要搞之前提到的 energy compensation，我们需要对上述函数进行修改，之前 energy compensation 中提到的 r 项如下：  

$$ r = \int_{\Omega} D(\omega_h) V(\omega_i, \omega_o) n \cdot \omega_i d \omega_i $$

我们可以改变之前 IBL 文章中对 Environment BRDF 的拆分方法：  

$$ \int_{\Omega} \cfrac {f_r}{F} (F_0 + (F_{90} - F_0)\alpha) n\cdot \omega_i d \omega_i $$
$$ \Rightarrow F_0 \int_{\Omega} DV n\cdot \omega_i d \omega_i + (F_{90} - F_0) \int_{\Omega} DV \alpha n\cdot \omega_i d \omega_i $$

可以看到 r 项就是前半部分，可以通过 envBRDF.x 获取，而之前的拆分方法是：  

$$ \Rightarrow F_0 \int_{\Omega} DV (1 - \alpha) n\cdot \omega_i d \omega_i + F_{90} \int_{\Omega} DV \alpha n\cdot \omega_i d \omega_i $$

那么在预计分代码上，我们需要做如下步骤：  

    float Fc = pow(1 - VoH, 5.0f);
    //r += (1.0 - Fc) * G_Vis;
    //g += Fc * G_Vis;
    r += G_Vis;
    g += Fc * G_Vis;

并且修改实现时更改代码为：  

    // 原来为 f0 * envBRDF.x + envBRDF.y 更改为 f0 * envBRDF.x + (1 - f0) * envBRDF.y
    indirectLight += prefilteredColor * lerp(envBRDFSpecular.yyy, envBRDFSpecular.xxx, standardPBRParams.F0) * energyCompensation;

最后生成的 Lut 图如下：  

<div  align="center">  
<img src="https://s2.loli.net/2024/10/31/hO8rNFEm4Xxoa9Y.jpg" width = "50%" height = "50%" alt="图12 - Environment Lut 图（包含了 diffuse 的预积分）"/>
</div>

注意：得到的 Lut 图的 Wrap Mode 最好设置为 Clamp，Filter Mode 最好设置为 Point。

> 我这张图的 Y 轴是 roughness，也就是一般认为的 linear roughness。而有些方案 Y 轴是 $\,\alpha\,$，即 linear roughness 的平方。

### 最终实现
IBL 最终实现代码和效果如下：  

    float3 calculateIBL(StandardPBRParams standardPBRParams,TextureCube prefilteredEnvMap, SamplerState prefilteredEnvMapSampler, Texture2D envLut, SamplerState envLutSampler, out float3 energyCompensation)
    {
        float3 envBRDF = SAMPLE_TEXTURE2D(envLut, envLutSampler, float2(standardPBRParams.NoV, standardPBRParams.roughness)).rgb;
        
        float3 irradiance = SampleSH(standardPBRParams.N);
        float3 envBRDFDiffuse = standardPBRParams.albedo * envBRDF.b;
        float3 Kd = 1.0 - standardPBRParams.metallic;

        float3 R = reflect(-standardPBRParams.V, standardPBRParams.N);
        float3 prefilteredColor = SAMPLE_TEXTURECUBE_LOD(prefilteredEnvMap, prefilteredEnvMapSampler, R, 8.0 * standardPBRParams.roughness).rgb;
        energyCompensation = 1.0 + standardPBRParams.F0 * (1.0 / envBRDF.x - 1) / 2;

        return irradiance * envBRDFDiffuse * Kd + prefilteredColor * lerp(envBRDF.yyy, envBRDF.xxx, standardPBRParams.F0) * energyCompensation;
    }

<div  align="center">  
<img src="https://s2.loli.net/2024/10/31/M24GAeX6Fj1Uhzb.jpg" width = "70%" height = "70%" alt="图13 - IBL 效果(不包含直接光）"/>
</div>