---
title: PBR 理论基础（BRDF）（二）
date: 2024-06-04 15:56:47
categories: 
  - [图形学]
tags:
  - 图形学
  - 游戏开发
top_img: /images/black.jpg
cover: https://s2.loli.net/2024/06/04/nouFeJyYMt179RE.jpg
mathjax: true
description: 本笔记的主要内容有 BRDF 的漫反射项和镜面反射项，以及对镜面反射项的法线分布函数和几何函数的详细介绍。
---

> 本篇文章主要参考了毛星云的文章：《基于物理的渲染（PBR）白皮书》。本来有了毛星云的文章不打算自己再另写一篇关于 PBR 理论知识的文章了，但是后来还是决定做出一定的更改、补充和简化，方便自己理解。本文章重点在于游戏的实时渲染，故重点对 BRDF 进行介绍，对 BTDF、BSDF 只有大概的介绍。  
> 
> 之前的《Unity Shader入门精要》读书笔记（五）中也有对 PBR 的介绍，建议结合起来看。
> 
> 推荐一个谷歌的 Filament PBR 白皮书：https://google.github.io/filament/Filament.md.html#materialsystem/diffusebrdf 
> 
> 本篇文章只包括直接光部分，不包括基于物理的环境光照，即间接光部分。间接光部分部分详见后面的 IBL 的文章。

# BRDF 模型各项
## Diffuse BRDF
Diffuse BRDF 主要包括以下几种方案：  
①Lambert [1760]  
②Oren Nayar [1994]  
③Simplified Oren-Nayar [2012]  
④Disney Diffuse [2012]（SIGGRAPH 2012 Physically-based shading at Disney）  
⑤Renormalized Disney Diffuse [2014]（SIGGRAPH 2014 Moving Frostbite to Physically Based Rendering）
⑥Gotanda Diffuse [2014]  
⑦PBR diffuse for GGX + Smith [2017]（GDC 2017 PBR Diffuse Lighting for GGX + Smith Microsurfaces）  
⑧MultiScattrering Diffuse BRDF [2018]（SIGGRAPH 2018 Material Advances in Call of Duty: WWII）  

我看了几篇知乎的文章，都比较推荐寒霜的 **Renormalized Disney Diffuse**：https://media.contentapi.ea.com/content/dam/eacom/frostbite/files/course-notes-moving-frostbite-to-pbr-v32.pdf ；https://www.ea.com/frostbite/news/moving-frostbite-to-pb

Renormalized Disney Diffuse 主要对 Disney(Burley) Diffuse 模型进行了改进，根据视角入射角对 Diffuse 进行补正来维持能量守恒，代码如下（没有乘 DiffuseColor 以及除以 π）：  

    float Fr_DisneyDiffuse (float NdotV, float NdotL, float LdotH, float linearRoughness)
    {
        float energyBias = lerp(0, 0.5 , linearRoughness);
        float energyFactor = lerp(1.0, 1.0 / 1.51 , linearRoughness);
        float fd90 = energyBias + 2.0 * LdotH * LdotH * linearRoughness;
        float3 f0 = float3(1.0f, 1.0f, 1.0f);
        float lightScatter = F_Schlick(f0, fd90, NdotL).r;
        float viewScatter = F_Schlick(f0, fd90, NdotV).r;

        return lightScatter * viewScatter * energyFactor ;
    }

    float3 F_Schlick (in float3 f0, in float f90, in float u)
    {
        return f0 + (f90 - f0) * pow (1.f - u , 5.f);
    }


## Specular BRDF
目前最主流的基于物理的镜面反射 BRDF 模型是基于微平面理论 microfacet theory 的 Microfacet Cook-Torrance BRDF。微平面理论即认为微面元的法线 m 等于 l 和 v 的一半，即半角向量 h（忘了就回去看《Unity Shader入门精要》读书笔记（五））。此 Specular BRDF 具有以下形式：

$$ f(l,v) = \cfrac {D(h)F(v,h)G(l,v,h)} {4(n \cdot l)(n \cdot v)} $$

$\,D(h)\,$：法线分布函数 Normal Distribution Function, NDF；$\,F(l, h)\,$: 菲涅尔方程 Fresnel Equation；$\,G(l, v, h)\,$: 几何函数 Geometry Function；分母 $\,4(n·l)(n·v)\,$：校正因子 Correction Factor，作为微观几何的局部空间和整个宏观表面的局部空间之间变换的微平面量的校正。

---

下图为 Specular BRDF 各项的不同模型方案（加粗为主流模型）：  

<div  align="center">  
<img src="https://s2.loli.net/2024/06/06/C5vgPZ1FUusJ8H2.png" width = "100%" height = "100%" alt="图15 - Specular BRDF 的不同模型"/>
</div>

菲涅尔项的 Schlick 的 Fresnel 近似式以及讲过很多次了，就不再提了，接下来详细介绍法线分布项以及几何项。

# Specular D
历史上主流的**法线分布函数 Normal Distribution Function，NDF**，按提出时间进行排序，可以总结为：  
①Berry [1923]  
②**Beckmann** [1963]  
③Phong [1973]  
④**Blinn-Phong** [1977]  
⑤ABC [1989]  
⑥**GGX** [2007] / **Trowbridge-Reitz** [1975]  
⑦Shifted Gamma Distribution，SGD [2012]  
⑧**Trowbridge-Reitz（GTR）**[2012]  
⑨Student’s T-Distribution , STD [2017]  
⑩Exponential Power Distribution , EPD [2017]  

法线分布函数是 PBR 中材质高光质感的决定因素，在 Microfacet Specular BRDF 的 D, G, F 三项中是最核心的一项。法线分布函数已从传统的 Blinn-Phong 等分布，迁移到更接近真实材质外观表现，具备能量守恒，具有更宽尾部和更高峰值的 GGX 分布，而且在朝着**多高光波瓣 multiple specular lobes** 的方向发展。

传统光照模型中，一般只将几何体建模到中尺度的**法线贴图 Normal Map** 层面。虽说 Blinn-Phong 等分布也是基于微平面理论推导而来，但并没有配套粗糙度贴图 Roughness Map 为其提供亚像素级精度的细节，而且传统的 NDF 一般都没有经过归一化，不满足能量守恒，容易出现失真。

在基于物理的渲染工作流中，通过将**粗糙度贴图 Roughness Map** 与微平面归一化的法线分布函数结合使用，将需渲染的几何体的建模尺度细化到了微观尺度 Microscale 的亚像素层面。

## 法线分布函数介绍
**法线分布函数 Normal Distribution Function，NDF** 也称为**微表面分布函数 Microfacet Distribution Function**，记为 $\,D(m)\,$。一般我们用宏观表面的半矢量 h 来表示微观表面法线 m，因为仅 m = h 的表面点的朝向才会将光线 l 反射到视线 v 的方向，其他朝向的表面点对 BRDF 没有贡献（正负相互抵消）。所以 **D(m)** 也写作 **D(h)**。

$\,D(m)\,$ 并不是真的概率密度函数，而是表示所有朝向 h 的微平面的面积。更准确的说 D(m) 表示：**单位面积，单位立体角的微平面的面积**。定量的描述为：

$$ D(h) = \cfrac {d^2A_h} {dAd \omega_h } $$

其中，$\,dA\,$ 表示宏观表面上的一小块区域，$\,d^2A_h\,$ 表示微平面的面积。对上式积分：

$$ \int_{\Omega} D(h) (n \cdot h) d \omega_h = \cfrac {1} {dA} \int (n \cdot h) d^2A_h $$

$\,\Theta\,$ 符号表示在在整个球体上积分。而在以 n 为中心的半球上积分时，用 $\,\Omega\,$ 表示。$\,(n \cdot h)d^2A_h\,$ 表示朝向 h 的微表面在宏观表面上的投影面积，那么将宏观表面上所有方向的微表面的投影面积加起来，刚好会等于 dA，即：  

$$ \int (n \cdot h) d^2A_h = dA $$

其几何意义如下图：  

<div  align="center">  
<img src="https://s2.loli.net/2024/06/17/2d6HZhRxveuXgbc.png" width = "40%" height = "40%" alt="图16 - D(h) 需要被归一化以满足微表面在宏观表面上的投影面积和宏观表面相等"/>
</div>

故为了保证物理准确性 D(h) 必须满足：

$$ \int_{\Omega} D(h) (n \cdot h) d \omega_h = 1 $$

---

另外 Walter 等人在论文 Microfacet Models for Refraction through Rough Surfaces 提出 D(h) 还需满足一个更强的限制条件：

$$ \int_{\Omega} D(h) (v \cdot h) d \omega_h = \cfrac {1} {dA} \int (v \cdot h) d^2A_h = n \cdot v $$

其中，v 是任意观察方向，当 v = n 时，该公式和上面公式是一样的，该式几何意义如下：

<div  align="center">  
<img src="https://s2.loli.net/2024/06/17/c8MtK2DRq1bSNeg.png" width = "50%" height = "50%" alt="图17 - 微观表面和宏观表面在垂直于任何视图方向 v 的平面上的投影是相等的"/>
</div>

$\,(v \cdot h) d^2 A_h = cos \theta d^2A\,$ 表示朝向 h 微表面在观察方向上的投影面积，其中有的面向 v 是正数，有的背朝 v 是负数；正负相加后，就刚好覆盖宏观表面上的单位区域 dAcosθ。

> 关于法线分布函数的更详细数学理解，不在这里阐述。有兴趣研究可以额外再去了解。其实作为使用者，而非学术研究者来说，对数学原理大致理解即可。


## 常见的法线分布函数
这里对其中在历史上使用和讨论较为广泛的几种法线分布函数，**Blinn-Phong**、**Beckmann**、**GGX (Trowbridge-Reitz)** 和 **GTR** 进行总结。

### Blinn-Phong 分布
Blinn-Phong 法线分布函数由 Blinn 推导出，作为（非基于物理的）Phong 着色模型的改进，以更好地拟合微平面 BRDF 的结构。Blinn-Phong 分布不具备形状不变性 shape-invariant。Blinn 在提出 Blinn-Phong 分布时没有指定归一化因子。加入归一化因子后的 Normalized Blinn-Phong 的公式如下：  

$$ D_p(m) = \cfrac {\alpha_p + 2} {2 \pi} (n \cdot m)^{\alpha_p} $$

其中 $\,\alpha^p\,$ 是 Blinn-Phong NDF 的**光滑度 smoothness** 参数。$\,\alpha^p\,$ 越大，表示微表面法线随机程度越小，表面越光滑；越小，微表面法线随机程度越大，表面更粗糙。对于非常光滑的曲面，值可以任意高（一个完美的镜面 $\,\alpha_p = \infty\,$），并且通过将 $\,\alpha^p\,$ 设置为 0 可以实现最大随机曲面（均匀 NDF），即兰伯特表面。

但这个 $\,\alpha^p\,$ 参数不便于艺术家操纵或直接绘制，因为它带来的视觉变化非常不均匀。这是因为 $\,\alpha^p\,$ 的变化与表面粗糙程度的变化并不呈线性关系。$\,\alpha^p\,$ 较小时，微小的变化会使表面粗糙程度剧烈变化；当 $\,\alpha^p\,$ 较大时，巨大的变化也不会使表面粗糙程度发生明显变化。出于这个原因，通常会定义**接口值 interface value**，艺术家调节这个接口值。例如：$\,\alpha^p=m^s\,$，其中 s 是 0 到 1 之间的艺术家接口值，m 是给定的电影或游戏中 $\,\alpha^p\,$ 的上限。这种映射被多款游戏使用，包括《使命召唤：黑色行动（Call of Duty: Black Ops）》，其中 m 被设置为值 8192。

在 UE4 中，则采用映射 $\,\alpha_p = 2\alpha^{-2} - 2\,$，那么得到的 Blinn-Phong 的形式为：  

$$ D_p(m) = \cfrac {1} {\pi \alpha^2} (n \cdot m) ^ {(\cfrac {2} {\alpha^2} - 2)} $$

UE4 中对 Blinn-Phong 的实现代码如下：  

    // [Blinn 1977, "Models of light reflection for computer synthesized pictures"]
    float D_Blinn( float a2, float NoH )
    {
        float n = 2 / a2 - 2;
        return (n+2) / (2*PI) * PhongShadingPow( NoH, n );    // 1 mad, 1 exp, 1 mul, 1 log
    }

---

上面的 Phong 分布只能表示**各项同性 isotropic** 材质表面的法线分布，也即围绕宏观法线 360 度的方向上，微表面分布相同，因此各项同性材质的 NDF 都是关于微观法线与宏观法线夹角 $\,\theta_h\,$ 的一元函数。

**各向异性 anisotropic** 材质在围绕宏观法线不同方向上，微表面分布不同。我们以宏观法线为 z 坐标，建立局部坐标系（**切线空间，x 轴为切线方向 t，y 轴为副法线方向 b**）：  

<div  align="center">  
<img src="https://s2.loli.net/2024/06/17/Qbd42oTRjv5mqsX.png" width = "50%" height = "50%" alt="图18 - 天顶角与方位角"/>
</div>

各项异性材质的 NDF 可以描述成关于天顶角 $\,\theta_h\,$ 与方位角 $\,\phi_h\,$ 的二元函数。Ashikhmin 和 Shirley 对 Phong 模型稍加改造，提出了一个各向异性的 BRDF：An Anisotropic Phong BRDF Model。从中我们可以得到一个各项异性的 Phong 模型：

$$ D_{p(aniso)}(h) = \cfrac {\sqrt{(\alpha_x + 2)(\alpha_y + 2)}}{2 \pi} cos \theta_h^{\alpha_x cos^2 \phi_h + \alpha_y sin^2 \phi_h} $$

其中，$\,\alpha_x\,$, $\,\alpha_y\,$ 表示沿着 x 方向和 y 方向的光滑度，当 $\,\alpha_x = \alpha_y\,$ 时退化为各项同性的 Phong 分布。下图的 $\,\alpha_x = \alpha_y\,$ 对应 $\,n_u = n_v\,$。

<div  align="center">  
<img src="https://s2.loli.net/2024/06/17/8MmSdXsOwUGn59L.png" width = "80%" height = "80%" alt="图19 - 不同指数下金属球的效果"/>
</div>

### Beckmann 分布
Beckmann 分布是光学业界开发的第一批微平面模型中使用的法线分布，是一种基于高斯函数的 NDF。也是 Cook-Torrance BRDF 在提出时选择的 NDF。Beckmann NDF 具备形状不变性 shape-invariant。正确归一化后， Beckmann 分布具有以下形式：  

$$ D_b(h) = \cfrac {1} {\pi \alpha_b^2 (n \cdot h)^4 } e^{\cfrac {(n \cdot h)^2 -1}{(n \cdot h)^2 \alpha_b^2}} = \cfrac {e^{-tan^2\theta_h/\alpha_b^2}} {\pi \alpha_b^2 cos^4 \theta_h}$$

当 $\,tan \theta_h\,$ 越大时， 表示该微表面法线更偏离宏观法线。而参数 $\,\alpha_b\,$ 可以表示表面的**粗糙度 roughness**，其本质是 $\,tan \theta_h\,$ 的**均方差 root mean square，RMS**。类似于高斯分布的方差，而宏观法线方向类似于高斯分布的数学期望。当 $\,\alpha_b\,$ 越大时，“方差”越大，越多的微表面法线偏离宏观法线，表面越粗糙；当 $\,\alpha_b\,$ 越小时，“方差”越小，越多的微表面法线朝向宏观法线，表面越光滑。注意，$\,\alpha_b\,$ 参数的意义和 Blinn-Phong 的 $\,\alpha_p\,$ 的意义刚好相反，我们可以用一个等式将两个参数关联起来：$\,\alpha_p = 2\alpha_b^{-2} - 2\,$。

Beckmann 分布在 $\,\alpha_b < 0.5\,$ 时与 Phong 分布非常相似，也即在表示光滑表面时，Beckmann 和 Phong 效果几乎一样。Beckmann 分布的优势在于表示粗糙的表面，Phong 模型有一个最大的粗糙度，也即 $\,\alpha_p = 0\,$ 时的兰伯特表面，此时微表面法线均匀分布。而实际上兰伯特表面并不是最粗糙的表面，比如一些带有尖刺的纤维材质比兰伯特材质更粗糙，这时 Beckmamn 分布就派上用场了，当 $\,\alpha_b > 1\,$ 时，大量的微表面 h 和 n 接近垂直，可以表示超级粗糙 super rough 的材质，比如天鹅绒。

UE4 中对 Beckmann 分布的实现代码如下：  

    // [Beckmann 1963, "The scattering of electromagnetic waves from rough surfaces"]
    float D_Beckmann( float a2, float NoH )
    {
        float NoH2 = NoH * NoH;
        return exp( (NoH2 - 1) / (a2 * NoH2) ) / ( PI * a2 * NoH2 * NoH2 );
    }

Beckmann 的各项异性版本的形式如下：  

$$ D_b(aniso)(h) = \cfrac {e^{-tan^2 \theta_h(cos^2 \phi_h/ \alpha^2_x + sin^2 \phi_h / \alpha_y^2)}} {\pi \alpha_x \alpha_y cos^4 \theta_h} = \cfrac {1}{\pi \alpha_x \alpha_y (n \cdot h)^4} e^{- \left(\cfrac {(x \cdot h)^2 / \alpha_x^2 + (y \cdot h)^2 / \alpha_y^2} {(n \cdot h)^2} \right)} $$

当 $\,\alpha_x = \alpha_y\,$ 时，又退化成各向同性的 Beckmann 分布。

以下为 UE4 中 Anisotropic Beckmann 分布的 Shader 实现代码：  

    // Anisotropic Beckmann
    float D_Beckmann_aniso( float ax, float ay, float NoH, float3 H, float3 X, float3 Y )
    {
        float XoH = dot( X, H );
        float YoH = dot( Y, H );
        float d = - (XoH*XoH / (ax*ax) + YoH*YoH / (ay*ay)) / NoH*NoH;
        return exp(d) / ( PI * ax*ay * NoH * NoH * NoH * NoH );
    }

### GGX (Trowbridge-Reitz) 分布
GGX 即 Trowbridge-Reitz 分布，最初由 Trowbridge 和 Reitz [Trowbridge 1975] 推导出，在 Blinn 1977年的论文 [Blinn 1977] 中也有推荐此分布函数，但一直没有受到图形学界的太多关注。30 多年后，Trowbridge-Reitz 分布被 Walter 等人独立重新发现 [Walter 2007]，并将其命名为 GGX 分布。

GGX 分布的公式为：  

$$ D_{GGX}(h) = \cfrac {\alpha_{GGX}^2} {\pi((n \cdot h)^2(\alpha_{GGX}^2 - 1) + 1)^2} $$

其中参数 $\,\alpha_{GGX}\,$ 与 $\,\alpha_b\,$ 功能类似，随着 $\,\alpha_{GGX}\,$ 的增加，表面越粗糙。GGX 与 Beckmann 分布类似，也能表示超级粗糙的表面，但相比于 Beckmann 和 Phong，GGX 分布有一大特点，即在 h 和 n 夹角接近 90 度时（掠射角），函数值不为 0，这意味着 GGX 的高光有更长的“拖尾”。

GGX 分布具备形状不变性 shape-invariant，而与其对标的 GTR 等分布不具备形状不变性，这是 GGX 能普及的深层次原因。

在迪士尼原理着色模型 Disney principled shading model 中，Burley 推荐将粗糙度控制以 $\,α = roughness^2\,$ 暴露给用户，其中 roughness 是 0 到 1 之间的用户界面粗糙度参数值，以让分布以更线性的方式变化。这种方式实用性较好，不少使用 GGX 分布的引擎与游戏都采用了这种映射，如 UE4 和 Unity。

    // GGX / Trowbridge-Reitz
    // [Walter et al. 2007, "Microfacet models for refraction through rough surfaces"]
    float D_GGX( float a2, float NoH )
    {
        float d = ( NoH * a2 - NoH ) * NoH + 1; // 2 mad
        return a2 / ( PI*d*d );         // 4 mul, 1 rcp
    }

### Generalized-Trowbridge-Reitz（GTR）分布
Burley [Burley 2012] 根据对 Berry，GGX 等分布的观察，提出了广义的 **Generalized-Trowbridge-Reitz，GTR** 法线分布函数，其目标是允许更多地控制 NDF 的形状，特别是分布的尾部：

$$ D_{GTR}(h) = k \cfrac {1} {((n \cdot h)^2(\alpha^2_{GTR} - 1) + 1)^{\gamma}} $$

其中，k 是归一化常数，$\,\gamma\,$ 是控制 GTR 形状的参数。不同的 $\,\gamma\,$ 和 $\,\alpha_{GTR}\,$ 有不同的归一化参数：  

$$ k(\alpha_{GTR}, \gamma) = \cfrac {(\gamma - 1)(\alpha_{GTR}^2 - 1)} {\pi (1 - (\alpha_{GTR}^2)^{1 - \gamma})} $$

当 $\,\alpha_{GTR} = 1\,$ 和 $\,\gamma = 1\,$ 时有奇点，针对这两种情况，用替代的归一化常数：  

$$ k(\alpha_{GTR}, 1) = \frac {\alpha_{GTR}^2 - 1} {\pi ln(\alpha_{GTR}^2)}, k(1, \gamma) = \cfrac {1} {\pi} $$

> 注意 glsl 和 hlsl 中 log(x) 函数是 e 底的对数，即 ln(x)。

当 $\,\gamma = 1\,$，GTR 即 Berry 分布；当 $\,\gamma = 2\,$，GTR 即 GGX 分布。随着 $\,\gamma\,$ 的值减小，分布的尾部变得更长。而随着 $\,\gamma\,$ 值的增加，分布的尾部变得更短。

---

Burley 也给出了 GGX 分布（$\,\gamma = 2\,$）各项异性的版本：

$$ \begin{align*} D_{GTR(aniso)}(h) &= \cfrac {1} {\pi \alpha_x \alpha_y(sin^2 \theta_h(cos^2 \phi_h / \alpha_x^2 + sin^2 \phi_h / \alpha_y^2) + cos \theta_h^2)^2} \\ &= \cfrac {1} {\pi \alpha_x \alpha_y((x \cdot h)^2 / \alpha_x^2 + (y \cdot h)^2 / \alpha_y^2 + (n \cdot h)^2)^2}\end{align*}\\ $$

而任意 $\,\gamma\,$ 值的各项异性的 GTR，Burley 在文章中并没有给出，因此没有解析式。

下面是 Disney 对 GTR 的实现：

    float GTR1(float NdotH, float a)
    {
        if (a >= 1) return 1/PI;
        float a2 = a*a;
        float t = 1 + (a2-1)*NdotH*NdotH;
        return (a2-1) / (PI*log(a2)*t);
    }

    float GTR2(float NdotH, float a)
    {
        float a2 = a*a;
        float t = 1 + (a2-1)*NdotH*NdotH;
        return a2 / (PI * t*t);
    }

    float GTR2_aniso(float NdotH, float HdotX, float HdotY, float ax, float ay)
    {
        return 1 / (PI * ax*ay * sqr( sqr(HdotX/ax) + sqr(HdotY/ay) + NdotH*NdotH ));
    }

### 补充：各项异性参数化方法
虽然参数化各向异性 NDF 的最直接的方法是使用各向同性粗糙度进行两次参数化，一次用于 $\,\alpha_x\,$，一次用于 $\,\alpha_y\,$。在迪士尼原理着色模型中，各向同性粗糙度参数 roughness 与艺术家接口参数 anisotropic（范围在 [0, 1]）相结合，从这些参数计算 $\,\alpha_x\,$ 和 $\,\alpha_y\,$ 的值：  

$$ aspect = \sqrt{ 1 - 0.9 anisotropic} $$
$$ \alpha_x = roughness^2 / aspect $$
$$ \alpha_y = roughness^2 * aspect $$

其中，上式中的因子 0.9 将纵横比限制为 10：1。

Sony Imageworks 则使用了一种不同的参数化，允许任意程度的各向异性：  

$$ \alpha_x = roughness^2 (1 + anisotropic) $$
$$ \alpha_y = roughness^2 (1 - anisotropic) $$

## 多高光波瓣
即便是 GGX 的高光长尾，仍然不足以媲美真实世界材质中的高光表现。在不增加 NDF 本身的复杂性的前提下，更好地匹配测量材质的替代解决方案是使用多个高光波瓣 multiple specular lobes。Cook 和 Torrance [Cook Torrance 1981] 首先提出了这个想法。之后 [Ngan 2005] 进行了实验测试，他发现对于大部分材质来说，添加第二个高光波瓣确实显着改善了贴合性。

Disney Principled BRDF 使用了两个固定的高光波瓣 specular lobe，且都使用 GTR 分布。 主波瓣 primary lobe 使用 $\,\gamma = 2\,$ 的 GTR（即 GGX 分布），代表基础底层材质 Base Material 的反射，可为各项异性 anisotropic 或各项同性 isotropic 的金属或非金属。次级波瓣 secondary lobe 使用 $\,\gamma = 1\,$ 的 GTR（即 Berry 分布），代表基础材质上的清漆层 ClearCoat Layer 的反射，一般为各项同性 isotropic 的非金属材质，即清漆层 ClearCoat Layer。

## 单次散射建模的局限性
目前实时渲染领域广泛采用的 microfacet BRDF 微平面模型，实际上是人们可以想到的最简单的模型，它仅对几何光学系统中的单层微表面上的单次散射进行建模。没有考虑多次散射，没有考虑衍射，也没有考虑波动光学。其假设所有遮挡的光线都被丢失，会导致与现实行为相比的能量损失。  

对此，一些论文提出，可以采用非基于物理的修正因子 corrective factors 来尝试对缺失的能量进行补偿。例如迪士尼模型中的的 Sheen 光泽项。  

也有不少渲染器结合 multiple-scattering BSDF 使用多次散射进行建模，如 blender 的 cycles 渲染器的 **Multiscatter GGX**（ https://developer.blender.org/D2002 ）。但目前的 multiple-scattering BSDF 方案主要为随机求解，所以不适用于实时渲染和游戏领域。


# Specular G
历史上主流的**几何函数 Geometry Function**，按提出时间进行排序，可以总结为： 
①Smith [1967]  
②V-cavity（Cook-Torrance）[1982]  
③Schlick-Smith [1994]  
④Neumann [1999]  
⑤Kelemen [2001]  
⑥Implicit [2010]  

Heitz 在 [Heitz 2014] 中进行了证明，在上述几种模型中仅如下两种是基于物理的：  
①**Smith 遮蔽函数（Smith masking function）**  
②**V 腔/Cook-Torrance 遮蔽函数（V-cavity/Cook-Torrance masking function）**

**Smith 遮蔽函数**是现在业界所采用的主流遮蔽函数，Eric Heitz 在 2014 年将其拓展为 **Smith 联合遮蔽阴影函数 Smith Joint Masking-Shadowing Function**，该函数具有四种形式：  
①分离遮蔽阴影型 Separable Masking and Shadowing；  
②高度相关遮蔽阴影型 Height-Correlated Masking and Shadowing；  
③方向相关遮蔽阴影型 Direction-Correlated Masking and Shadowing；  
④高度-方向相关遮蔽阴影型 Height-Direction-Correlated Masking and Shadowing。

其中，**高度相关遮蔽阴影型 Height-Correlated Masking and Shadowing**，以及其近似，是目前业界采用的主流遮蔽阴影函数。

---

几何函数是保证 Microfacet BRDF 理论上能量守恒，逻辑上自洽的重要一环。其表示微平面同时被入射方向和反射方向可见（没有被遮挡的）的比例。在 Microfacet Specular BRDF 的 D, G, F 三项中，如果说法线分布函数是最核心的一项，那么几何函数则是核心的辅助项，而且是三项中最复杂的一项。

通常，除了近掠射角或非常粗糙的表面，几何函数对 BRDF 的形状影响相对较小，但对于 BRDF 保持能量守恒而言，几何函数至关重要。几何函数取决于微表面的细节，并且很少有精确的表达式。很多情况下，各类文献会使用各种统计模型和简化假设推导出近似值。

几何函数与法线分布函数具有紧密的联系：  
①**几何函数的解析形式的确认依赖于法线分布函数**。在微平面理论中，通过可见微平面的投影面积之和等于宏观表面的投影面积的恒等式，选定法线分布函数，并选定几何函数的模型，就可以唯一确认几何函数的准确形式。在选定几何函数的模型后（一般为Smith），几何函数的解析形式的确认则由对应的法线分布函数决定。  
②**法线分布函数需要结合几何函数，得到有效的法线分布强度**。几何函数是对能顺利完成入射和出射的微平面概率进行建模的函数。法线分布函数需要结合几何函数，得到最终对 microfacet BRDF 能产生贡献的有效法线分布强度。

## 几何函数介绍
几何函数 $\,G(l,v,h)\,$ 和分母中的校正因子 $\,4(n·l)(n·v)\,$ 会合并，为**可见性项 The Visibility Term**，V 项。

几何函数具有两种主要形式：$\,G_1\,$ 和 $\,G_2\,$，其中：  
①$\,G_1\,$ 为微平面在单个方向（光照方向 L 或观察方向 V）上可见比例，微表面对光照方向不可见，称为**自阴影**，用**阴影函数 shadowing function** 描述，即 $\,G_1(o)\,$。微表面对视角方向不可见，称为**自遮挡**，用**遮蔽函数 masking function** 描述，即 $\,G_1(v)\,$。  
②$\,G_2\,$ 为微平面在光照方向 L 和观察方向 V 两个方向上可见比例，用**联合遮蔽阴影函数 joint masking-shadowing function** 描述，可以表示为两个方向的可见性函数的乘积，即 $\,G(o,i) = G_1(o)G_1(i)\,$。

<div  align="center">  
<img src="https://s2.loli.net/2024/06/18/rQpzjF3Ev9ByKHq.png" width = "60%" height = "60%" alt="图20 - 自阴影（左）与自遮挡（右）"/>
</div>

### 遮蔽函数
先讨论自遮挡，自遮挡是因为背朝 back-facing 观察方向的微表面挡住了面朝 forward-facing 观察方向的微表面，而对着色有贡献的是面朝观察方向未被遮挡，且朝向 h 的微表面。并且，所有背朝观察方向的微表面在观察方向上的投影面积和它遮挡的面朝观察方向的微表面在在观察方向上的投影面积是相等的，简单地讲，即 forward-facing 减去 back-facing 就等于可见的（未被遮挡的） forward-facing。

根据法线分布函数中提到的一个限制公式：  

$$ \int_{\Omega} D(h) (v \cdot h) dw_h = n \cdot v $$

该公式表达了表示朝向 h 微表面在观察方向上的投影面积，其中有的面向 v 是正数，有的背朝 v 是负数；正负代数相加后，就刚好覆盖宏观表面上的单位投影区域 dAcosθ。

那么我们规定一个可见性函数 $\,G_1(h, v)\,$ 表示朝向 h 的微表面从 v 看过去未被遮挡的比例。$\,G_1(h, v)\,$ 的值域是 [0, 1]。我们不难得到 $\,G_1(h, v)\,$ 满足：  

$$ \int_{\Omega} G_1(h, v) max(0, v \cdot h) D(h) dw_h = G_1(h, v) \int_{\Omega}max(0, v \cdot h) D(h) dw_h \\= \int_{\Omega} D(h) (v \cdot h) dw_h = n \cdot v $$

其中 $\,max(0, v \cdot h)\,$ 表达了面朝 forward-facing 观察方向的微表面；$\,G_1(h, v)\,$ 表达了可见的（未被遮挡的）的比例；$\, \int_{\Omega} D(h) (v \cdot h) dw_h = n \cdot v \,$ 表达了面朝 forward-facing 观察方向的微表面的总投影面积和背朝 back-facing 观察方向的微表面的总投影面积的代数和。

我们把面朝 forward-facing 观察方向的微表面的总投影面积记作 $\,A^+(v)\,$，背朝 back-facing 观察方向的微表面的总投影面积记作 $\,A^-(v)\,$，那么上面的公式可以写作：  

$$ G_1(h, v)A^+(v) = A^+(v) - A^-(v) = n \cdot v $$

那么可见性函数的定义可以写成：  

$$ G_1(h, v) = \cfrac {A^+(v) - A^-(v)} {A^+(v)} = \cfrac {n \cdot v} {A^+(v)} $$

可见性函数可以被传统地表达为一个辅助函数 $\,\Lambda(v)\,$，它表达了被遮挡不可见的投影区域占可见的投影区域的比例：  

$$ \Lambda(v) = \cfrac {A^-(v)} {A^+(v) - A^-(v)} = \cfrac {A^-(v)} {cos \theta} = \cfrac {1 - G_1(h, v)} {G_1(h, v)} $$

可以得到：

$$ G_1(h,v) = \cfrac {1} { 1 + \Lambda(v) } $$

---

我们可以根据法线分布函数和公式 $\,\int_{\Omega} G_1(h, v) max(0, v \cdot h) D(h) dw_h = cos \theta\,$ 推导出几何函数。然而一个特定法线分布函数不能为该公式提供足够的条件去计算出一个特定的几何函数，可以有无数个几何函数满足公式的约束，它仅告诉我们有多少百分比的微平面的法线指向了某些方向，而没有告诉我们这些法线是如何进行排列。

为了确定唯一的 G 项，Heitz 在 [Heitz 2014] 中引入了第二个约束：选择合适的微表面轮廓 microsurface profile，从而对 G 项进行具象化建模。一旦选择好合适的微表面轮廓，即选择了合适的微表面模型，加上上面等式的约束，就可以完全确定遮蔽函数，用于 BRDF 中的实际使用。

Heitz 还在 [Heitz 2014] 中证明了本节开头说的几种模型中仅 Smith 遮蔽函数和 V 腔遮蔽函数是基于物理的。同时 Heitz 还证明了 **Smith 遮蔽函数**是唯一既遵循公式，又具有法线遮蔽独立性 normal-masking independence 便利特性的函数。且 Smith 遮蔽函数具有比 Cook-Torrance 使用的 V 腔遮蔽函数 V-cavity masking function 更好地匹配真实世界的反射现象，这也是业界在两种基于物理的遮蔽函数中，更加青睐 Smith 遮蔽函数的原因。如下图所示： 

<div  align="center">  
<img src="https://s2.loli.net/2024/06/19/8dpZMja6Q7PSvWh.webp" width = "60%" height = "60%" alt="图21 - 使用 V-cavity 和 Smith 遮蔽阴影函数的各向同性 Beckmann 分布产生的 BRDF，以及 Reference BRDF 的对比。"/>
</div>

### Smith 遮蔽函数
最初的 Smith 函数是为 Beckmann NDF 所设计，但 Brown [Brown 1980] 和后来的 Bourlier 等人 [Bourlier 2002] 将 Smith 函数推广到计算几何函数以匹配任何 NDF 的方法中。

Smith $\,G_1\,$ 函数的形式如下：  

$$ G_1(h, v) = \cfrac {\chi^+(h, v)} {1 + \Lambda(v)} $$

其中

$$ \chi^+(x) = \begin{cases} 1, & \text{where x > 0} \\ 0, & \text{where x $\leq$ 0} \end{cases} $$

而之前的：  

$$ G_1(h,v) = \cfrac {1} { 1 + \Lambda(v) } $$

是 Smith 遮蔽函数的广义形式 [Brown 1980; Walter 2007]。对于许多随机曲面，其具有闭合形式的解。因此，在法线/遮蔽独立 normal/masking independence 的假设下，Smith 遮蔽函数是精确的。

Smith 遮蔽函数的每个法线分布函数会导出不同的 $\,\Lambda\,$（lambda）函数。需要注意，仅具有形状不变性的法线分布函数（如 GGX、Beckmann）可以导出具有解析形式的 $\,\Lambda\,$ 函数，而且不具备形状不变性的函数（如 Blinn-Phong），则 $\,\Lambda\,$ 不存在解析形式。在 [Walter 2007] 和 [Heitz 2014] 中都有描述推导出给定 NDF 的 $\,\Lambda\,$ 的过程。

法线/遮蔽独立 Normal/Masking Independence 作为 Smith 遮蔽函数的一大特点，源自 Smith 微表面轮廓假设微曲面不是自相关的 autocorrelated，即在微表面的一个点处的高度（或法线）与任何相邻点处的高度（或法线）之间没有相关性。这意味着一组随机的微平面并不是连续的表面，微表面的高度和法线是独立的随机变量。

Smith 遮蔽函数确实也有一些缺点。从理论角度来看，其假设是与实际表面的结构不一致的，甚至可能在物理上无法实现。从实际的角度来看，虽然它对于随机表面非常准确，但是对于在法线方向和遮蔽之间具有更强依赖性的表面，其预期精度会降低，特别是如果表面具有一些重复结构（如多数面料）。然而，在找到更好的替代方案之前，Smith 遮蔽函数目前依然是业界的最佳选择。

### Smith 联合遮蔽-阴影函数
自阴影本质与自遮挡是一样，只有在朝向入射光方向，可见的微表面才对着色有贡献。只需要把 $\,G_1(h,v)\,$ 的 v 替换为 l 就行，即 $\,G_1(h,l)\,$。这样就从遮蔽函数变为了阴影函数。

若微表面模型的自遮挡和自阴影是相互独立的，可以表示为两个方向的可见性函数的乘积，即

$$ G_2(h,l,v) = G_1(h,l) G_1(h,v) $$

Eric Heitz 在 [Heitz 2014] 中提出的 **Smith 联合遮蔽阴影函数 Smith Joint Masking-Shadowing Function**，即 $\,G_2(h,l,v)\,$，具有四种形式：  
①分离的遮蔽阴影型 Separable Masking and Shadowing  
②高度相关的遮蔽阴影型 Height-Correlated Masking and Shadowing  
③方向相关的遮蔽阴影型 Direction-Correlated Masking and Shadowing  
④高度-方向相关遮蔽阴影型 Height-Direction-Correlated Masking and Shadowing

***①分离的遮蔽阴影函数***  
最简单和最广泛使用的遮蔽阴影函数的变体是 Walter 等人推广的可分离形式 [Walter 2007]。 在这种情况下，遮蔽 masking 和阴影 shadowing 是独立的，并且分别计算并相乘：  

$$ \begin{align*} G_2(h,l,v) &= G_1(h,l)G_1(h,v) \\ &= \cfrac {\chi^+ (h, l)} {1 + \Lambda(l)} \cfrac {\chi^+ (h,v)} {1 + \Lambda(v)} \end{align*} $$

***②高度相关的遮蔽阴影函数***  
更精确形式的遮蔽阴影函数模拟了由于微表面高度引起的遮蔽和阴影之间的相关性 [Ross et al. 2005]。微平面在微表面内升高得越多，对于出射方向未被遮蔽和入射方向未被掩蔽的可见概率会同时增加。因此，遮蔽和阴影通过微平面的升高而相关。这种相关性以联合遮蔽阴影函数的高度相关形式进行表达：  

$$ G_2(h,l,v) = \cfrac {\chi^+ (h, l) \chi^+ (h,v)} {1 + \Lambda(l) + \Lambda(v)} $$

Heitz 建议在实践中使用此版本的遮蔽阴影函数，因为它比可分离的遮蔽阴影函数更精确，却同时具有相同的计算复杂度。但需要注意的是，当出射方向和入射方向彼此远离时，此形式是准确的，但是当方向接近时，此形式会估算出更多的阴影。

***③方向相关的遮蔽阴影函数***  
Ashikhmin 等 [Ashikhmin 2000] 通过混合可分离的遮蔽阴影函数与两个方向完全相关的情形来表达方向相关：  

$$ G_2(h,l,v) = \lambda(\phi) G_1(h, l) G_1(h, v) + (1 - \lambda(\phi)) min(G_1(h, l) G_1(h, v)) $$

其中 $\,\lambda(\phi)\,$ 是经验因子。  

***④高度方向相关的遮蔽阴影函数***  
遮蔽和阴影之间的方向相关可以通过将方向相关因子 λ 纳入高度相关形式来进行建模：  

$$ G_2(h,l,v) = \cfrac {\chi^+ (h, l) \chi^+ (h,v)} {1 + max(\Lambda(l), \Lambda(v)) + \lambda(l,v) min(\Lambda(l),\Lambda(v))} $$

这里，当出射和入射方向平行且 λ = 0 时，遮蔽和阴影完全相关。相关性随着方向之间的角度增加而减小，并且随着 λ 增加到 1，遮蔽和阴影不再是方向相关，公式会返回高度相关的形式。

### 多重散射
通过包含 $\,G_2\,$ 函数，Microfacet BRDF 能够考虑遮蔽 masking 和阴影 shadowing，但依然没有考虑微平面之间的互反射 interreflection，或多表面反射 multiple surface bounce。而缺少微平面互反射是业界主流 Microfacet BRDF 的共有的限制。

即标准 Microfacet BRDF 模型的最大问题，是虽然它们能量守恒（即不会产生任何能量），但它们也不能在高粗糙度时维持能量。这是由于建模微平面模型时所做出的单散射假设，没有模拟微表面上的多次散射，即缺少微平面互反射 interreflection。单散射的在高粗糙度时会有较大的能量损失，从而显得过暗。

GGX + height-correlated masking & shadowing，在粗糙度 1 时会损失接近一半的入射能量。

对此 Heitz 在 [Heitz 2016] 中深入讨论了多重散射微平面 BRDF Multiple-scattering microfacet BRDF，但该文章仅介绍了多尺度 BRDF 的随机计算，并不适用于实时渲染。  

随后，Sony ImageWork 的 Kulla 和 Conty [Kulla 2017] 在 SIGGRPAPH 2017 上中提出了一项新的技术方案，创建一个模拟多次反射表面反射的附加 BRDF 波瓣，作为**能量补偿项 Energy Compensation Term**。

> 以后有需求再额外了解，这里不详细介绍了。

以下为有无 Imagework 基于能量补偿的多反射镜面反射项 multiple-bounce specular term 的渲染对比图：  

<div  align="center">  
<img src="https://s2.loli.net/2024/06/19/PrRizg1TBNqoCKA.png" width = "70%" height = "70%" alt="图22 - GGX 单散射（上）和 GGX 单散射 + 能量补偿（下）对比图"/>
</div>

### 几何函数的性质
①标量性  
几何函数是 0 和 1 之间的一个标量。低粗糙度下几何函数大多情况下数值接近 1，增加表面粗糙度（更高的值）会使函数更快地降至零。如下图所示：  

<div  align="center">  
<img src="https://s2.loli.net/2024/06/19/fSmJa75lHu23nzF.webp" width = "50%" height = "50%" alt="图23 - GGX 对应的遮蔽阴影函数"/>
</div>

②对称性  
几何函数在两个可见方向（出射和入射）上是对称的：  

$$ G(h, l, v) = G(h, v, l) $$

③同向可见性  
几何函数从宏观表面正面方向上无法看到微表面的背面，反之亦然。

④拉伸不变性  
微表面几何轮廓具有拉伸不变性（Stretch Invariance）。拉伸微表面轮廓就像拉伸一张图片，即在一个维度上乘以常数因子，不会更改微表面轮廓的拓扑结构：拉伸后，遮挡的光线仍会被遮挡，未遮挡的光线仍未被遮挡。  

当微表面轮廓中涉及的所有斜率同时缩放时，遮蔽概率对于配置拉伸是不变的。这包括微表面的斜率和与出射方向相关的斜率。它们都是通过拉伸因子的倒数来缩放的。因此，斜率宽度的分布也被反向拉伸因子拉伸。

## 主流法线分布函数导出的 Smith 遮蔽函数
### Beckmann 法线分布的 Λ 函数
Beckmann 法线分布函数具备形状不变性，存在 Smith 遮蔽函数对应的 Λ 解析形式，为：  

$$ \Lambda(v) = \cfrac {1} {2} \left( erf(a) - 1 + \cfrac {e^{-a^2}} {a \sqrt{\pi} } \right) $$

其中 $\, a = \cfrac {1}{\alpha tan \theta_o} \,$ 。

上式的计算成本很高，因为它包括误差函数 erf，于是 Walter 等人 [Walter et al. 2007] 为其提出了精确的有理逼近，可以用于近似 $\,\Lambda(v)\,$：  

$$ \Lambda(v) \approx \begin{cases} \cfrac {1 - 1.259a + 0.396a^2} {3.535a + 2.181a^2}, & \text{if a < 1.6} \\ 0, & \text{otherwise.} \end{cases} $$

### GGX 法线分布的 Λ 函数
GGX 法线分布函数具备形状不变性，其 Smith 遮蔽函数对应的 Λ 解析形式相对简单，为：  

$$ \Lambda(v) = \cfrac {-1 + \sqrt{1 + \alpha^2 tan^2 \theta}} {2} $$

**GGX 分布和 GGX – Smith 遮蔽阴影函数的组合，是目前游戏和电影业界主流的方案**。且业界一直致力于优化两者的组合。下面将对 **Smith GGX** 的演变和发展做一个总结。

## 工业界对 Smith GGX 的选用
游戏和电影工业对 GGX-Smith 遮蔽函数的选用方面，可以总结为两个主要阶段：  
①SIGGRAPH 2014 之前，Smith 分离的遮蔽阴影函数；  
②SIGGRAPH 2014 之后，Smith 相关的遮蔽阴影函数；

而这两个阶段的演变，主要在于 2014 年 Eric Heitz 于 JCGT 2014 上发表了著名的 paper 《Understanding the Masking-Shadowing Function in Microfacet-Based BRDFs》，以及其后续在 SIGGPRAPH 2014 上进行的同名的 talk。

### Smith 分离的遮蔽阴影函数
#### SIGGRAPH 2012：Disney
Disney 参考了 [Walter 2007] 的近似方法，使用 Smith GGX 导出的 G 项，并将粗糙度参数进行重映射以减少光泽表面的极端增益，α 的值为 $\,(0.5 + roughness/2)^2\,$，从而使几何项的粗糙度变化更加平滑，更便于美术人员的使用。

以下为 Disney 实现的 Smith GGX 的几何项的表达式：  

$$ \alpha_g = (0.5 + roughness/2)^2 $$
$$ G_1(v) = \cfrac {2(n \cdot v)} {(n \cdot v) + \sqrt{\alpha_g^2 + (1 - \alpha_g^2)(n \cdot v)^2}} $$

$$ G_2(l,v,h) = G_1(l)G_1(v) $$

下面是 Disney 对 $\,G_1\,$ 的实现：

    float smithG_GGX(float NdotV, float alphaG)
    {
        float a = alphaG*alphaG;
        float b = NdotV*NdotV;
        return 1 / (NdotV + sqrt(a + b - a*b));
    }

之所以返回的分子是 1，是因为 $\,G_1(l)\,$ 和 $\,G_1(v)\,$ 的分子相乘会和 Specular BRDF 的分母 $\,4(n \cdot l)(n \cdot v)\,$ 抵消。

各项异性的 GGX 的遮蔽函数如下：  

$$ \begin{align*} G_{1}(v) &= \cfrac {2(n \cdot v)} {n \cdot v + \sqrt{(n \cdot v)^2 + (\alpha_x^2 cos^2 \phi_v + \alpha_y^2 sin^2 \phi_v)(1 - (n \cdot v)^2)}} \\ &= \cfrac {2(n \cdot v)} {n \cdot v + \sqrt{(n \cdot v)^2 + \alpha_x^2 (x \cdot v)^2 + \alpha_y^2 (y \cdot v)^2}} \end{align*} $$

下面是 Disney 对各项异性版的实现：  

    float smithG_GGX_aniso(float dotVN, float dotVX, float dotVY, float ax, float ay)
    {
        return 1.0 / (dotVN + sqrt(pow(dotVX * ax, 2.0) + pow(dotVY * ay, 2.0) + pow(dotVN, 2.0)));
    }

#### SIGGRAPH 2013：UE4
其中 UE4 在 SIGGRAPH 2013 上公布的方案为基于 Schlick 近似，将 k 映射为 k = a/2，去匹配 GGX Smith 方程，并采用了 Disney 对粗糙度的重映射：  

$$ k = \alpha / 2 $$
$$ \alpha = (0.5 + roughness/2)^2 $$
$$ G_1(v) = \cfrac {(n \cdot v)} {(n \cdot v)(1 - k) + k} $$
$$ G(l,v,h) = G_1(l)G_1(v) $$

### Smith 相关的遮蔽阴影函数
在 2014 年之后，都转向了 Smith 联合遮蔽阴影函数 The Smith Joint Masking-Shadowing Function 的高度相关遮蔽阴影形式 Height-Correlated Masking and Shadowing，并相应地都做了一些近似与优化，即：  

$$ G_2(l,v) = \cfrac {1} {1 + \Lambda(l) + \Lambda(v)} $$

如果 $\,G_1\,$ 选择适配 GGX 的方程，那么该改进方程为：  

$$ G_2(l,v) = \cfrac {2(l \cdot n)(v \cdot n)} {(l \cdot n) \sqrt{(v \cdot n)^2(1 - \alpha_g^2) + \alpha_g^2} + (v \cdot n) \sqrt{(l \cdot n)^2(1 - \alpha_g^2) + \alpha_g^2}} $$

上式计算开销比较大，因此在实时渲染中可以使用一个近似方程：

$$ G_2(l,v) = \cfrac {2(l \cdot n)(v \cdot n)} {(l \cdot n) ((v \cdot n)(1 - \alpha_g) + \alpha_g) + (v \cdot n) ((l \cdot n)(1 - \alpha_g) + \alpha_g)} $$

上近似方程也常写成线性插值的形式：  

$$ G_2(l,v) = \cfrac {2(l \cdot n)(v \cdot n)} {lerp(2(v \cdot n)(l \cdot n), v \cdot n + l \cdot n, \alpha} $$

#### Frostbite 的 GGX-Smith Joint 近似方案
Frostbite 的 Lagarde[Lagarde 2014] 观察到对 GGX 高度相关的 Smith G2 height-correlated Smith G2 具有与镜面微平面 BRDF 的分母组合时抵消的项, 因此可以简化组合项为：  

$$ \cfrac {G_2(l,v)} {4 |n \cdot l| |n \cdot v|} \Rightarrow \cfrac {0.5} {\mu_o \sqrt{\alpha^2 + \mu_i(\mu_i - \alpha^2 \mu_i)} + \mu_i \sqrt{\alpha^2 + \mu_o(\mu_o - \alpha^2 \mu_o)}} $$

其中，$\,\mu_i = (n \cdot l)^+\,$，$\,\mu_o = (n \cdot v)^+\,$。

#### UE4 的 GGX-Smith Correlated Joint 近似方案
UE4 采用的 GGX-Smith Correlated Joint Approximate 为：

$$ \alpha = roughness^2 $$
$$ \Lambda(v) = (l \cdot n) ((v \cdot n)(1 - \alpha) + \alpha) $$
$$ \Lambda(l) = (v \cdot n) ((l \cdot n)(1 - \alpha) + \alpha) $$
$$ \cfrac {G_2(l,v,h)} {4 (n \cdot l) (n \cdot v)} = \cfrac {0.5} {\Lambda(v) + \Lambda(l)} $$

实现代码如下：  

    // Appoximation of joint Smith term for GGX
    // [Heitz 2014, "Understanding the Masking-Shadowing Function in Microfacet-Based BRDFs"]
    float Vis_SmithJointApprox( float a2, float NoV, float NoL )
    {
        float a = sqrt(a2);// a2 = Pow4( Roughness )
        float Vis_SmithV = NoL * ( NoV * ( 1 - a ) + a );
        float Vis_SmithL = NoV * ( NoL * ( 1 - a ) + a );
        return 0.5 * rcp( Vis_SmithV + Vis_SmithL );
    }

#### Unity HDRP 的 GGX-Smith Correlated Joint 近似方案
Unity HDRP 采用的 GGX-Smith Correlated Joint Approximate 为：  
$$ \alpha = roughness $$
$$ \Lambda(v) = (l \cdot n) ((v \cdot n)(1 - \alpha) + \alpha) $$
$$ \Lambda(l) = (v \cdot n) \sqrt{(l \cdot n)^2(1 - \alpha^2) + \alpha^2} = (v \cdot n) \sqrt{(-(n \cdot l) \alpha^2 + n \cdot l)(n \cdot l) + \alpha^2} $$
$$ \cfrac {G_2(l,v,h)} {4 (n \cdot l) (n \cdot v)} = \cfrac {0.5} {\Lambda(v) + \Lambda(l)} $$

实现代码如下：  

    // Note: V = G / (4 * NdotL * NdotV)
    // [Heitz 2014, "Understanding the Masking-Shadowing Function in Microfacet-Based BRDFs"]
    float V_SmithJointGGX(float NdotL, float NdotV, float roughness, float partLambdaV)
    {
        float a2 = Sq(roughness);

        // Original formulation:
        // lambda_v = (-1 + sqrt(a2 * (1 - NdotL2) / NdotL2 + 1)) * 0.5
        // lambda_l = (-1 + sqrt(a2 * (1 - NdotV2) / NdotV2 + 1)) * 0.5
        // G        = 1 / (1 + lambda_v + lambda_l);

        // Reorder code to be more optimal:
        float lambdaV = NdotL * partLambdaV;
        float lambdaL = NdotV * sqrt((-NdotL * a2 + NdotL) * NdotL + a2);

        // Simplify visibility term: (2.0 * NdotL * NdotV) /  ((4.0 * NdotL * NdotV) * (lambda_v + lambda_l))
        return 0.5 / (lambdaV + lambdaL);
    }

    float V_SmithJointGGX(float NdotL, float NdotV, float roughness)
    {
        float partLambdaV = GetSmithJointGGXPartLambdaV(NdotV, roughness);
        return V_SmithJointGGX(NdotL, NdotV, roughness, partLambdaV);
    }

    float GetSmithJointGGXPartLambdaVApprox(float NdotV, float roughness)
    {
        float a = roughness;
        return NdotV * (1 - a) + a;
    }

#### Google Filament 渲染器的 GGX-Smith Joint 近似方案
Google Filament 渲染器采用的 GGX-Smith Correlated Joint Approximate 为：  
$$ \alpha = roughness $$
$$ \Lambda(v) = (l \cdot n) \sqrt{(v \cdot n)^2(1 - \alpha^2) + \alpha^2} = (l \cdot n) \sqrt{(-(n \cdot v) \alpha^2 + n \cdot v)(n \cdot v) + \alpha^2} $$
$$ \Lambda(l) = (v \cdot n) \sqrt{(l \cdot n)^2(1 - \alpha^2) + \alpha^2} = (v \cdot n) \sqrt{(-(n \cdot l) \alpha^2 + n \cdot l)(n \cdot l) + \alpha^2} $$
$$ \cfrac {G_2(l,v,h)} {4 (n \cdot l) (n \cdot v)} = \cfrac {0.5} {\Lambda(v) + \Lambda(l)} $$

实现代码如下：  

    float V_SmithGGXCorrelated(float NoV, float NoL, float a)
    {
        float a2 = a * a;
        float GGXL = NoV * sqrt((-NoL * a2 + NoL) * NoL + a2);
        float GGXV = NoL * sqrt((-NoV * a2 + NoV) * NoV + a2);
        return 0.5 / (GGXV + GGXL);
    }

# 白炉测试与弱白炉测试
Eric Heitz 在 [Heitz 2014] 中提出了白炉测试与弱白炉测试，作为 BSDF 和 BRDF 是否能量守恒的标准化测试方法。

<div  align="center">  
<img src="https://s2.loli.net/2024/06/20/xlRb4WzivqdNPpn.png" width = "70%" height = "50%" alt="图24 - GGX 白炉测试（上）和 GGX + Energy Compensation 白炉测试（下），可以看到“下”基本可以保持能量守恒"/>
</div>

## 白炉测试
**白炉测试 The White Furnace Test** 的思路为验证入射总能量为1的光线的反射光线分布是否归一化的测试。  

可以将白炉测试理解为一束辉度 irrandiance 为 1 的光线从上往下照到菲涅尔反射项为 1 的材质表面，并测定反射光线总能量是否为 1 的过程。若反射光线总能量为 1，则通过白炉测试，否则，便不通过白炉测试。在白炉测试中，菲涅尔反射项始终为 1，则光线永远不会透射，即所有的入射光线都会被全部反射，则表示投射的光线将被散射一次或多次并最终离开表面。

## 弱白炉测试
因为在 2014 年白炉测试被提出时，主流的 BRDF 模拟微表面上的单次反射，并不能多次散射，具有能量损失。所以业界主流的 BRDF 模型即使在“完美的反射表面 perfect reflector”微表面上进行参数化时也不会积分到 1，并且不满足白炉测试方程。对此，Eric Heitz 在 [Heitz 2014] 中设计了另一个限制性较小的，用于测试常见的基于 microfacet 的 BRDF 是否标准化的测试：**弱白炉测试 The Weak White Furnace Test**。

弱白炉测试 The Weak White Furnace Test 的主要思路为验证在第一次反弹之后和离开表面之前反射的光线分布是否归一化。对几何项而言，即没有入射的阴影 shadowing 项，只有出射的遮蔽 masking 项。可以通过用遮蔽 masking 函数替换遮蔽阴影 masking-shadowing 函数。

> 因为白炉测试比较学术，本篇文章强调应用，故不在这里多加阐述了。