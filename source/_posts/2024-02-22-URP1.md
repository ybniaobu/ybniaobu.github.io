---
title: Unity URP 和 HLSL（一）
date: 2024-02-22 14:03:56
categories: 
  - [数学, 3d数学基础]
  - [数学, 线性代数]
tags:
  - 游戏开发
  - 数学
  - 线性代数
top_img: /images/black.jpg
cover: https://s2.loli.net/2024/02/18/pMAzYioaFZEkS8I.gif
mathjax: true
---

> 之前的《Unity Shader入门精要》是以 Unity 内置渲染管线为基础的，以 Cg 语言为主，但当使用 URP 和 HDRP 时，使用的是 HLSL 语言；  
> 本文主要介绍 URP 相关概念以及 HLSL 主要语法，并将部分重要的 Unity Shader 入门精要的 Cg 语言的 Shader 转换为 HLSL，以实现 NPR 渲染为结尾。

# Unity SRP
**SRP**，**Scriptable Render Pipeline**，即**可编程渲染管线**。Unity 当前提供两个预先构建的 SRP： ①**URP**，**Universal Render Pipeline**，即**通用渲染管线**；②**HDRP**，**High Definition Render Pipeline**，即**高清渲染管线**。SRP 可以理解为是一个 API 层，允许使用 C# 脚本来调度和配置渲染命令。Unity 将这些命令传递给它的低级图形架构，后者随后将指令发送给图形 API。

URP 和 HDRP 都可以使用 **Shader Graph** 来编写 Shader，但是手写 Shader 在性能方面有很大的优势，因此在项目中前期建议直接使用 Shader Graph 来编写 Shader，因为 Shader Graph 可视化更加便捷，在得到想要的效果后，在项目后期改成手写 Shader 来提高性能。

## SRP 和内置管线的不同
在 SRP 中手写 Shader 仍然使用的是 Shader Lab，和内置渲染管线相比，结构并没有太大的变化，主要的几点不同如下：  
**①**使用 **HLSL** 而不是 Cg 语言，尽管这不是强制的，但是 HLSL 是官方推荐的。Cg 语言已经不再更新。官方的 URP 的自带的 Shader 都是 HLSL 编写的，URP Shader 代码中会调用 URP Shader Library 中的 Shader 代码（在名为 Universal RP 的 Packages 中）；  
**②**SubShader 的 Tags 中需要指定 **RenderPipeline**，例如：`"RenderPipeline" = "UniversalPipeline"`表示这是一个 URP 的 Shader；  
**③**Pass 的 Tags 中的 **LightMode** 和内置渲染管线不同，在内置管线中，LightMode 表示了这个 Pass 处于管线中处理光照的哪个步骤，例如 `ForwardBase`，`ForwardAdd`分别是前向流水线中的主光照 pass 和附加逐像素光照 pass。而 URP 中，我们经常使用的有 `UniversalForward`，`ShadowCaster`，`DepthNormalsOnly`，`DepthOnly`，`SRPDefaultUnlit` 等 pass；  
**④**为了兼容 **SRP Batcher**，Shader 需要采用一些特殊的写法。简单来说，就是对于 Properties 中的属性，需要放到特定的 **CBuffer** 中，这个 CBuffer 的名字为 **UnityPerMaterial**。另外对于 Unity 的内置属性，要放到 **UnityPerDraw** 的 CBuffer 中；  
**⑤**如果要支持 **GPU Instancing**，Shader 代码还要使用另外的一组宏来修改，使得可以支持 Instancing。这组宏其实也是支持 SRP Batcher 的。


## URP
**【待补充】**
Render Pipeline Asset  

URP ShaderLab Pass tags：https://docs.unity3d.com/Packages/com.unity.render-pipelines.universal@17.0/manual/urp-shaders/urp-shaderlab-pass-tags.html  




# URP shader 基础框架

> 不要管代码块标注的 c，只是为了让代码高亮

``` C
Shader "Example/URPUnlitShaderBasic" {
    Properties {
        [MainColor] _BaseColor("Base Color", Color) = (1, 1, 1, 1)
        [MainTexture] _BaseMap("Base Map", 2D) = "white"{}
    }

    SubShader {
        //SubShader Tags 定义何时以及在何种条件下执行某个 SubShader 代码块或某个 Pass。
        Tags { "RenderType" = "Opaque" "RenderPipeline" = "UniversalPipeline" }

        Pass {
            //声明 Pass 名称，方便调用与识别
            Name "ForwardUnlit" 

            HLSLPROGRAM

            #pragma vertex vert
            #pragma fragment frag

            //Core.hlsl 文件包含常用的 HLSL 宏和函数的定义，还包含对其他 HLSL 文件（例如Common.hlsl、SpaceTransforms.hlsl 等）的 #include 引用。
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"

            struct Attributes {
                //positionOS 即 position in object space
                float4 positionOS   : POSITION;
                float2 uv           : TEXCOORD0;
                half3 normal        : NORMAL;
            };

            struct Varyings {
                //positionHCS 即 position in homogeneous clip space 齐次裁剪空间
                float4 positionHCS  : SV_POSITION;
                float2 uv           : TEXCOORD0;
                half3 normal        : NORMAL;
            };

            //材质单独声明，使用 DX 风格的新采样方法，下面两个宏都在 Core.hlsl 里面
            //此宏将 _BaseMap 声明为 Texture2D 对象。
            TEXTURE2D(_BaseMap);
            //此宏声明采样器
            SAMPLER(sampler_BaseMap);

            //要使 Unity 着色器 SRP Batcher 兼容，请在名为 UnityPerMaterial 的单个 CBUFFER 代码块中声明与材质相关的所有属性。
            CBUFFER_START(UnityPerMaterial)
                half4 _BaseColor;
                float4 _BaseMap_ST;
            CBUFFER_END

            Varyings vert(Attributes IN) {
                Varyings OUT;
                OUT.positionHCS = TransformObjectToHClip(IN.positionOS.xyz);
                //TRANSFORM_TEX 宏在 Macros.hlsl 定义，用于应用纹理偏移缩放
                OUT.uv = TRANSFORM_TEX(IN.uv, _BaseMap);
                OUT.normal = TransformObjectToWorldNormal(IN.normal);
                return OUT;
            }

            half4 frag(Varyings IN) : SV_Target {
                //SAMPLE_TEXTURE2D 宏用于纹理采样
                half4 color = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, IN.uv);
                color *= _BaseColor;
                return color;
            }
            ENDHLSL
        }
    }
}
```